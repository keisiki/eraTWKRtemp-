;
;### 関数 ###################################
;以下の関数は「映姫様口上」内の「M_KOJO_K30_関数・お説教ライブラリ」を拝借したものです
;はたて口上作者が作成したものではないのでご注意ください
;同ライブラリの利用に関しては、映姫様口上の作者様のライセンスに準じます

;拝借ここから
;==================================================

;==================================================

;==================================================
;MASTERの연인がその場に居るかどうか返す式中関数
;これは力技でなんとか実装できたっぽいので、使わなくても問題ない気がする（小町口上作者）
;でも絶対こっちのがスマート
;=-1 小町が연인 =0 元から연인なし =1 その場に居ない =2 연인と同席
@K42_FIND_LOVER()
#FUNCTION
SIF TALENT:MASTER:연인 == 0
	RETURNF 0
SIF TALENT:연인
	RETURNF -1
LOCAL = TALENT:MASTER:연인
SIF CFLAG:LOCAL:현재위치 == CFLAG:MASTER:현재위치
	RETURNF 2
RETURNF 1
;==================================================


;==================================================
;同部屋のキャラを拾う式中関数。探す順位は[연인]持ち→知り合い→TARGET内ランダム
;アリス口上の関数お借りして平文化した映姫口上の関数を拝借しました。つまり丸パクり
;丸コピする以外の技術がないので先人たちの知恵に感謝をし続けるしかない
@K42_FIND_AROUND()
#FUNCTION
#DIM 知り合い, 6 = 29, 65, 51, 49, 38, 1
#DIM 총인원
#DIM 抽選
#LOCALSIZE 200
;はたての知り合いは6人,文>椛>にとり>>>古明地姉妹>霊夢の順に(勝手に)設定
;一応理由としては
;文→原作でしっかり絡みがある
;椛→原作で名前を言っている、文と比べて邪険にされていない、身内と言及している
;にとり→名前は出てないけど河童の技術力は称賛してるし、ガラケー修理したのがにとりだといいなあという勝手な願望
;古明地姉妹→サトリ（種）に対してまったく嫌悪感抱いてないどころか心を読んでとかはたてちゃん大天使
;霊夢→文が興味を持つのも分かると言っていた（ような覚えがある）
VARSET LOCAL
抽選 = 0
총인원 = GET_TARGETNUM() 
;部屋に一人なら当然0
SIF 총인원 == 1
	RETURNF 0
;[연인]持ちがいたら優先してIDを返す
SIF K42_FIND_LOVER() == 2
	RETURNF TALENT:MASTER:연인
;지인 우선도 순으로 계획을 알아보고 MASTER와 같으면 아이디를 반환한다
FOR COUNT, 0, 6
	SIF CFLAG:(知り合い:COUNT):현재위치 == CFLAG:MASTER:현재위치
		RETURNF 知り合い:COUNT
NEXT
;知り合いがいなければ同室のTARGET_ID全部を拾う
FOR COUNT,1, 총인원 + 1
	;地獄の断頭台withジェロニモ状態を防ぐため口上主はスキップ
	SIF TARGET:COUNT == TARGET:0
		CONTINUE
	;MASTERとはたてちゃんの현재위치にいなければスキップ
	SIF (CFLAG:(TARGET:COUNT):현재위치 != CFLAG:MASTER:현재위치) || (CFLAG:(TARGET:COUNT):현재위치 != CFLAG:42:현재위치)
		CONTINUE
	;配列のカウントは別で取りながら代入
	LOCAL:抽選 = TARGET:COUNT
	抽選 ++
NEXT
;抽選してIDを決定
RETURNF LOCAL:(RAND(抽選))
;==================================================


;==================================================
;酔っぱらい度を数字で返す式中関数
;=0 素面 =1 ほろ酔い =2 酔いどれ 3=ぐでぐで
@K42_DRUNK()
#FUNCTION
SIF BASE:42:술기운 <= 2
	RETURNF 0
SELECTCASE BASE:42:술기운
	;ぐでぐで
	CASE IS > (MAXBASE:42:술기운 / 10) * 8
		RETURNF 3
	;酔いどれ
	CASE IS > (MAXBASE:42:술기운 / 10) * 5
		RETURNF 2
	;ほろ酔い
	CASE IS <= (MAXBASE:42:술기운 / 10) * 5
		RETURNF 1
ENDSELECT
;==================================================
;이름을 알려주는 이벤트(함수)
;K39ひねくれナズー口上様より拝借
;==================================================

@NAME_SET_K42
PRINTL 이름을 입력해 주세요(직접 입력하거나, 버튼으로 선택해주세요)
IF NAME:MASTER != "당신"
	LOCALS = %NAME:MASTER%
ELSE
	LOCALS = %CALLNAME:MASTER%
ENDIF
PRINTBUTTON @"[이름(%LOCALS%)을 대다]", @"%LOCALS%"
PRINTL 　
;名前を入力するループ
$INPUT_LOOP_ONAMAE
INPUTS
; 行頭行尾の空白文字削除
LOCALS = %RESULTS%
REPLACE LOCALS, "\(\^\\s\+\|\\s\+\$)", ""
;10文字以上は受け付けない
IF STRLENS(LOCALS) > 10
	CLEARLINE 1
	REUSELASTLINE 「저기、그거 진짜 네 이름이야?　거짓말한 거 아냐? 너무 길잖아」
	GOTO INPUT_LOOP_ONAMAE
;空白は受け付けない
ELSEIF RESULTS == ""
	PRINTL 이름을 입력해 주세요
	GOTO INPUT_LOOP_ONAMAE
ELSE
	; 동성명
	IF FLAG:역극 != 42 && STRCOUNT(LOCALS, "\(히메카이도\)\*\\s\*하타테")
		CLEARLINE 1
		REUSELASTLINE 「어、설마 똑같은 이름?　그럴리가 없지」
		GOTO INPUT_LOOP_ONAMAE
	; うんそうだね多々良勝五郎とか教える人もいるかもしれないもんね
	ELSEIF (FLAG:역극 != 29 && STRCOUNT(LOCALS, "\(샤메이마루\)\*\\s\*아야")) || (FLAG:역극 != 51 && STRCOUNT(LOCALS, "\(카와시로\)\*\\s\*니토리")) || (FLAG:역극 != 65 && STRCOUNT(LOCALS, "\(이누바시리\)\*\\s\*모미지"))
		CLEARLINE 1
		REUSELASTLINE 「어、너 인간인줄 알았는데 실은 요괴였던 거야?　그럴리가 없지」
		GOTO INPUT_LOOP_ONAMAE
	; 직접 입력하면 될 줄 알았나? 튕길 테다！
	ELSEIF FIRSTTIME("U42-1") && ABL:친밀 < 4 && STRCOUNT(LOCALS, "\(お\(\(오빠\|오라버니\)\|\(언니\|ねえ\)\)\(さん\|ちゃん\)\)\$")
		CLEARLINE 1
		REUSELASTLINE 「그럴리가 없잖아」
		GOTO INPUT_LOOP_ONAMAE
	ELSEIF FIRSTTIME("U42-1") && ABL:친밀 < 7 && STRCOUNT(LOCALS, "\(お\(\(오빠\|오라버니\)\|\(姉\|ねえ\)\)\(님\|님\)\)\$")
		CLEARLINE 1
		REUSELASTLINE 「뭐?」
		GOTO INPUT_LOOP_ONAMAE
	ELSEIF FIRSTTIME("U42-1") && (ABL:순종 < 7 || ABL:봉사정신 < 7) && STRCOUNT(LOCALS, "\(\(御\|ご\)\(주인\|주인\)\(님\|님\)\)$")
		CLEARLINE 1
		REUSELASTLINE 「뭐? 그 발상, 징그러워」
		GOTO INPUT_LOOP_ONAMAE
	ELSEIF  FIRSTTIME("U42-1") && (!TALENT:연인 || !TALENT:연모) && STRCOUNT(LOCALS, "\(\(남편\|남편\)\(님\|님\)\)\$")
		CLEARLINE 1
		REUSELASTLINE 「뭐? 그 발상, 징그러워」
		GOTO INPUT_LOOP_ONAMAE
	ELSE
		PRINTFORML 「%RESULTS%…(이)네」
	ENDIF
	PRINTL [0] 네
	PRINTL [1] 아니요
	;入力した名前を確定するループ
	$INPUT_LOOP_KAKUTEI_1
	INPUT
	IF RESULT == 0
		;あなたの名前を%MASTERNAME:42%に格納
		MASTERNAME:42 = %RESULTS%
		;K42NAME = %RESULTS%/에러가 나와서 봉인
		PRINTFORML 
	ELSEIF RESULT == 1
		PRINTL 이름을 입력해 주세요
		GOTO INPUT_LOOP_ONAMAE
	ELSE
		PRINTL 이름을 입력해 주세요
		GOTO INPUT_LOOP_ONAMAE
	ENDIF
ENDIF
;拝借ここまで

;==================================================
;ここから一応自作関数
;==================================================
;본체 함수를 가공하여 자작 함수로
;==================================================
;발기상태를 숫자로 반환하는 식중 함수
;=0 通常 =1 むっくり =2 ビンビン =3 Erection!!
@K42_BOKKI()
#FUNCTION
#DIM CHARA
IF BASE:CHARA:발기 == MAXBASE:CHARA:발기
	RETURNF 3
ELSEIF BASE:CHARA:발기 >= 1000
	RETURNF 2
ELSEIF BASE:CHARA:발기 >= 500
	RETURNF 1
ELSE
	RETURNF 0
ENDIF

;==================================================
;会えなかった日を算出する関数
;==================================================
@K42_AENAI
#DIM SAIGO
SAIGO = CFLAG:1003
IF (TALENT:연모 || TALENT:연인) && SAIGO > 6
	PRINTFORML 「드디어 만났어」
	PRINTFORML 하타테는 %CALLNAME:MASTER%의 모습을 발견하자 그녀는 조심스럽게 목밑에 살며시 안겨왔다
	PRINTW 
	PRINTFORML 「{SAIGO}일만인걸。계속 못봐서 서운했어」
	PRINTFORML 항상 밝고 활기찬 하타테의 목소리는 들어 본 적이 없을 정도로 연약했다
	PRINTW 
ENDIF
RETURN 0
;==================================================
;交際を始めてからの日数を表示する関数
;==================================================
@K42_KOUSAI
#DIM KOIBIT
KOIBIT = DAY:0 - CFLAG:1004
IF KOIBIT % 124 == 0 && KOIBIT
	PRINTFORML 「오늘로 우리가 사귄 지 {KOIBIT / 124}년이 되어가네」
	SIF CFLAG:MASTER:초기위치 == CFLAG:42:초기위치
		PRINTW 「한 지붕 아래서 살거나」
	SIF EXP:출산경험 > 0
		PRINTW 「가족이 늘거나」
	PRINTFORML 「여러가지 일들이 있었지」
	SIF TALENT:처녀 == 1
		PRINTW 아、그런데도 아직 내 처음은 안받아줬네。어떻게 된거야??」
	PRINTFORML 「앞으로도 잘 부탁해、%MASTERNAME:42%。」
	PRINTW 
ENDIF
RETURN 0

;==================================================
;個人情報の「キャラ紹介」の文章を書き換える関数
;==================================================
;@CHARA_INFO_KOJO_K42()
;個人情報の「キャラ紹介」の文章を書き換える
;これがあれば「CSTR」の20番以降(20～39)に文字列を書き込むだけで書き換わるはず
;早苗口上、諏訪子口上より引用、改変
;-----------------------------------------------------------------------------------------------
@CHARA_INFO_KOJO_K42()
;===============================================================================================
;記述例
;このコメントアウトを外すと「CSTR」の20番以降に文字列を書き込むだけで書き換わるようになる
;FOR LOCAL,20,40
;	SIF CSTR:42:(LOCAL) != ""
;		PRINTFORML %CSTR:31:(LOCAL)%
;NEXT
;RETURN 1
;…とまあ、こんな感じでCSTR20番以降を使えばいいはず
;===============================================================================================
;本口上は、書き換わる文章が多いのでCSTRへの保存をせずに直接書き込む
;現時点で下の2行が書き込まれている
;PRINTL □ キャラ紹介 □---------------------------------------------------------------------
;PRINTL 

;##################################################################################
;①「～今どきの念写記者～　●種族：鴉天狗　●能力：念写をする程度の能力」をプリント
;##################################################################################
CALL M_KOJO_COLOR_K42
PRINTFORML %CSVCSTR(42, 10)%
RESETCOLOR
PRINTL 

;##################################################################################
;②キャラ紹介文を書き込み
;##################################################################################
;結婚イベント
IF CFLAG:K42C_結婚 >= 1
	PRINTFORML %CALLNAME:MASTER%와(과) 종족의 울타리를 넘어 맺어졌다
	PRINTFORML 왼손의 약지에는 결혼 반지가 끼워져 있다
	PRINTFORML %CALLNAME:MASTER%에 대한 사랑은 의심할 여지도 없다
	PRINTL 
ELSE
	IF TALENT:42:연모 && TALENT:42:연인
		PRINTFORML 요즘의 카라스텐구는 좋아하게 되면 전력을 다한다
		PRINTFORML %CALLNAME:MASTER%와(과) 부부가 되기를 내심 바라는 것 같은데…?
		PRINTL 
		IF EXP:42:애정경험 >= 200
			PRINTFORML %CALLNAME:MASTER%와(과)의 애정은 충분히 충족되어 있다
		ELSE
			PRINTFORML 아직도 %CALLNAME:MASTER%와(과) 사랑을 깊게 하고 싶어하는 것 같다
		ENDIF
		
		IF EXP:42:데이트경험 >= 100
			PRINTFORML 데이트를 여러 번 거듭하면서, 추억을 많이 만들었다
		ELSE
			PRINTFORML 아직도 %CALLNAME:MASTER%와(과) 데이트로 많은 추억을 만들고 싶은 것 같다
		ENDIF
		
		IF CFLAG:42:호감도 >= 5000
			PRINTFORML %마스터를% 좋아하는 것을 공언하여 거리낌이 없다
		ELSE
			PRINTFORML 좀 더 %마스터를% 좋아하고 싶어하는 것 같다
		ENDIF
	ENDIF
ENDIF

;임신소망イベント
{
	IF DAY:2 == 4 && GROUPMATCH(DAY:3, 4, 14, 24) && ESTRUS_CYCLE(TARGET) >= 70 && !TALENT:임신소망 && (TALENT:연모 || TALENT:연인)
	 && (TALENT:MASTER:성별 == 2 || TALENT:MASTER:성별 == 3) && CFLAG:호감도 >= 10000 && EXP:애정경험 >= 500 && !TALENT:임신 && !TALENT:육아중
 }
		PRINTFORML %CALLNAME:MASTER%와(과)의 아이를 갖고 싶은 마음이 전례 없을 정도로 강해지고 있는 것 같다
	ELSE
	ENDIF

;時姦バレイベント
IF CFLAG:42:1413 == 1
	PRINTL 
	PRINTFORML 멈춘 시간속에서 계속 범해진 신체는 완전히 %CALLNAME:MASTER%에게 굴복해버렸다
	IF CFLAG:K42C_結婚 >= 1
		PRINTFORML 그래도, 사랑하는 남편으로부터의 행위이기에 모든 것을 받아들이고 있는 것 같다
	ELSEIF TALENT:연모 || TALENT:연인
		PRINTFORML 그래도, 사랑하는 사람의 행위라는 것을 알고 당황하면서도 받아들이는 것 같다
	ELSEIF TALENT:애욕 || TALENT:섹프
		PRINTFORML 새로운 자극이 되자, 오히려 환영하는 듯하다
	ELSE
		PRINTFORML 멋대로 몸이 괴롭혀지는 혐오감과 쾌감으로 복잡한 심경인 것 같다
	ENDIF
	PRINTL 
ELSEIF CFLAG:42:1414 == 1
	PRINTL 
	PRINTFORML 눈치채지 못하는 사이에 절정을 이루는 불가해한 현상…
	PRINTFORML 그 때는, 항상 %마스터가% 눈 앞에 있었다
	PRINTFORML %마스터가% 뭔가 관련이 있는 걸까?
	PRINTFORML 하타테는 그 밖에 같은 피해자가 없는지 취재를 시작하기로 했다
	PRINTL 
	
	IF EXP:42:무자각절정경험 >= 500
		PRINTFORML 자신을 마음대로 유린하고 있음으로써 범인은 완전히 방심하고 있다고 믿고 있는 것 같다
		PRINTFORML 한편, 돌이킬 수 없는 곳까지 신체가 조련된 것을 깨닫지 못하고 있다
	ELSEIF EXP:42:무자각절정경험 >= 250
		PRINTFORML 범인에게 잘 지목되었다고 생각하고 있다. 좀 더 방심시켜야겠다고 생각하고 있는 것 같다
		PRINTFORML 한편으로는, 몸은 굴복할 것 같은 상태라는 사실을 외면하고 있다
	ELSE
		PRINTFORML 방심하고 있는 척을 하면, 자신을 미끼로 범인과 접촉할 수 있다고 생각하는 것 같다
		PRINTFORML 결코 쾌락을 위해서가 아니라고 자신에게 타이르고 있다
	ENDIF
	
	IF CFLAG:42:1015 >= 50
		PRINTFORML 거듭되는 속옷 피해는 %CALLNAME:MASTER%의 소행이라고 확신하고 있는 것 같다
	ELSEIF CFLAG:42:1015 >= 25
		PRINTFORML 거듭되는 속옷 피해는 %CALLNAME:MASTER%의 소행이 아닌가 하고 의심이 깊어지고 있다
	ELSE
		PRINTFORML 속옷 피해는 %CALLNAME:MASTER%에 의한 것인지, 자신이 없는 것 같다
	ENDIF
	
	IF CFLAG:42:1011 >= 100
		PRINTFORML 피해를 당한 소녀들로부터 수많은 증언을 얻었다
		PRINTFORML 취재는 이제 충분하다고 느끼고 있는 것 같다
	ELSEIF CFLAG:42:1011 >= 50
		PRINTFORML 소녀들의 피해는 생각보다 심각한 것 같다
		PRINTFORML 취재도 중요한 고비라고 생각하고 있다
	ELSEIF CFLAG:42:1011 >= 1
		PRINTFORML 피해자는 또 있었다
		PRINTFORML 아직 확신을 얻기까지는 이르지 못했다. 좀 더 취재를 계속해야겠다고 생각하고 있다
	ELSE
		PRINTFORML 취재는 전혀 진척되지 않은 것 같다…
	ENDIF
ENDIF

;裏花果子念報イベント
	IF TALENT:섹프 || TALENT:애욕
		IF CFLAG:42:1008 >= 100
			PRINTFORML 취재를 통해, 신문의 방향이 결정되었다。환상향 최초의 풍속지를 간행하기로 했다
		ELSEIF CFLAG:42:1008 >= 50
			PRINTFORML 취재를 통해, 자신이 쓰고 싶은 신문이 무엇인지 아련한 이미지가 파악되기 시작한 것 같다
		ELSE
			PRINTFORML 새로운 신문의 형태를 모색하고 있는 것 같은데, 이미지가 떠오르지 않는 것 같다
		ENDIF
	ENDIF
PRINTL 
RETURN 1
