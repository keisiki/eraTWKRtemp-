;FileName_情事発覚.ERB --------------------------------- Rev1.00
;情事の発覚
;CALL		USER
;ARG		0:入室キャラNO, 1:発覚状況(0:동실, 1:覗き)
;RETURN		0:동실, 1:立ち去る
;COMMENT	
;-----------------------------------------------------------
@AFFAIR_DISCLOSURE(ARG, NOZOKI)
#DIM PREV_TARGET
#DIM IRAI_ABLE
#DIM NOZOKI
#DIM FORCE_STOP
#DIM FORCE_GETOUT
#DIM WITNESSED

VARSET LOCAL
FORCE_STOP = 0
FORCE_GETOUT = 0

WITNESSED = TFLAG:현재의TARGET
SIF CFLAG:MASTER:장난
	WITNESSED = TARGET
IF WITNESSED && (CFLAG:(WITNESSED):우후후 || CFLAG:(WITNESSED):장난) && !GETBIT(FLAG:잠복,0)
	DRAWLINE
	IF CFLAG:(WITNESSED):장난
		PRINTFORMW %CALLNAME:ARG%에게 잠든 %CALLNAME:(WITNESSED)%한테 장난치는 걸 보여져버렸다!
		IF !CFLAG:ARG:약점잡음 && CFLAG:(WITNESSED):장난 == 2
			PRINTFORMW %CALLNAME:ARG%에게 약점을 잡혔다
		ELSE
			PRINTFORMW %ARG는% 어이없는 표정을 하고 있다
		ENDIF
		CALL TOUCH_SET(0,0,0,1)
		DRAWLINE
		FORCE_STOP = 1
		CFLAG:ARG:약점잡음 += 10
	ELSEIF TFLAG:102 == 3 && TFLAG:껴안기모드
		PRINTFORMW %CALLNAME:ARG%에게 %조사처리(CALLNAME:(TFLAG:현재의TARGET),"와")% 는실난실 거리는 걸 보여져버렸다!
		PRINTFORMW %조사처리(CALLNAME:(TFLAG:현재의TARGET),"는")% 유감스럽게  %CALLNAME:MASTER%에게서 떨어졌다…
		CALL UWAKI_RECORD(ARG,0,TFLAG:현재의TARGET)
		TFLAG:껴안기모드 = 0
		TFLAG:102 = 1
		CALL 덮쳐지기종료
	ELSE
		IF NOZOKI
			PRINTFORMW 【%NAME_FROM_PLACE(CFLAG:ARG:현재위치)%】에 있던 %CALLNAME:ARG%에게 %조사처리(CALLNAME:(TFLAG:현재의TARGET),"와")%의 정사를 보여져버렸다!
		ELSE
			PRINTFORMW %조사처리(CALLNAME:(TFLAG:현재의TARGET),"와")%의 정사 현장에 %ARG가% 들어왔다!
		ENDIF
		CALL UWAKI_RECORD(ARG)
		;恋人同士（もしくは恋人を含む）場合
		IF TALENT:MASTER:연인 && (TALENT:(TFLAG:현재의TARGET):연인 || WITH_연인())
			;出くわした
			IF !NOZOKI
				[SKIPSTART]
				;恋慕かある程度仲が良い
				IF (CFLAG:ARG:6 > 1 || TALENT:ARG:연모)
					PRINTFORMW %ARG는%は気まずそうにしている…
					PRINTL [0] まとめて押し倒す
					PRINTL [1] やめておく
					$INPUT_EMBRACE
					INPUT
					IF RESULT == 0
						IF !NOZOKI
							PRINTFORMW %CALLNAME:MASTER%が手招きすると%ARG는%は嬉しそうに体を摺り寄せてきた
							CFLAG:ARG:우후후 = CFLAG:(WITNESSED):우후후
							CFLAG:ARG:동실 = 1
							;見た人
							CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,4)
							;見られた人（最も地位の高いTARGETで代表）
							CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,4)
						ENDIF
					ELSEIF RESULT == 1
						PRINTFORMW %ARG는%は%마스터와%と目が合うとそそくさとその場を去った…
						IF !NOZOKI
							;見た人
							CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,3)
							;見られた人（最も地位の高いTARGETで代表）
							CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,3)
						ENDIF
						IF CFLAG:ARG:현재위치 == CFLAG:ARG:전턴위치
							CALL GETOUT(ARG)
						ELSE
							CFLAG:ARG:현재위치 = CFLAG:ARG:전턴위치
						ENDIF
						;キャラを動かしたのでTARGET再設定
						CALL TARGETSET_CHACK
						CFLAG:ARG:동실 = 0
						RETURN 1
					ELSE
						CLEARLINE 1
						GOTO INPUT_EMBRACE
					ENDIF
				;参戦するほど仲は良くない
				ELSE
				[SKIPEND]
				;入ってきた方が恋人だった場合、割り込んでくる
;				IF TALENT:MASTER:연인 && TALENT:MASTER:연인 == TALENT:ARG:연인
				IF TALENT:ARG:연인
					PRINTFORMW %ARG는% 질세라 %CALLNAME:MASTER%에게 몸을 문질렀다
					CFLAG:ARG:우후후 = CFLAG:(WITNESSED):우후후
					;恋人が他の女といちゃついてたら、理性吹っ飛んでもおかしくないし、テンションマックスって意味でムードが上がってもいいんじゃないかなぁと愚考する次第
					;BASE:ARG:무드 = MAXBASE:ARG:무드
					;BASE:ARG:이성 = 0
					;上記がコメントアウトされてるけど、それが原因のバグが出てるっぽいので追記
					;まあ、マックス＆0にするのはさすがにやり過ぎだった気がしないでもない
					SIF BASE:ARG:무드 < 300
						BASE:ARG:무드 = 300
					SIF BASE:ARG:이성 > 300
						BASE:ARG:이성 = 300
					;見た人
					CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,2)
					CALL UWAKI_RECORD(ARG,1)
					;見られた人（最も地位の高いTARGETで代表）
					;CALL KOJO_EVENT(4,WITNESSED,ARG)
					CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,2)
					CALL UWAKI_RECORD(WITNESSED, 1)
					DRAWLINE
					RETURN 0
				;してる方が恋人だった場合、そそくさと立ち去る（恋人が混じっている多人数プレイかつTARGETが恋人でない場合も立ち去る
				ELSE
					PRINTFORMW %ARG는% %마스터와% 눈이 마주치자 허둥지둥 그 자리를 떠났다…
					IF !NOZOKI
						;見た人
						CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,3)
						;見られた人（最も地位の高いTARGETで代表）
						CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,3)
					ENDIF
					IF CFLAG:ARG:현재위치 == CFLAG:ARG:전턴위치
						CALL GETOUT(ARG)
					ELSE
						CFLAG:ARG:현재위치 = CFLAG:ARG:전턴위치
					ENDIF
					;キャラを動かしたのでTARGET再設定
					CALL TARGETSET_CHACK
					CFLAG:ARG:동실 = 0
					RETURN 1
				ENDIF
			;覗きみた
			ELSE
				PRINTFORMW %ARG는% 어색한 듯 고개를 돌리더니 허둥지둥 그 자리를 떠났다…
				IF !NOZOKI
					;見た人
					CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,3)
					;見られた人（最も地位の高いTARGETで代表）
					CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,3)
				ENDIF
				IF CFLAG:ARG:현재위치 == CFLAG:ARG:전턴위치
					CALL GETOUT(ARG)
				ELSE
					CFLAG:ARG:현재위치 = CFLAG:ARG:전턴위치
				ENDIF
				;キャラを動かしたのでTARGET再設定… まぁ、覗きなんで大丈夫なはず。
				CALL TARGETSET_CHACK
				CFLAG:ARG:동실 = 0
				RETURN 1
			ENDIF
		;ここから恋人が含まれていない場合
		ELSEIF (TALENT:ARG:음란 || TALENT:ARG:걸레)
			PRINTFORMW %마스터가% 손짓하자 %ARG는% 기쁜 듯이 몸을 문질렀다
			CFLAG:ARG:우후후 = CFLAG:(WITNESSED):우후후
			CFLAG:ARG:동실 = 1
			;見た人
			CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,4)
			;見られた人（最も地位の高いTARGETで代表）
			CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,4)
		ELSEIF TALENT:ARG:무지 && !(CFLAG:ARG:기정사실 & 합의_우후후)
		;無知かつうふふ未経験
			IF NOZOKI
				PRINTFORMW %ARG는% %NAME_FROM_PLACE(CFLAG:ARG:현재위치)%에서 %CALLNAME:MASTER%들의 행위를 \@ TALENT:ARG:수치심 > 0 ?  얼굴이 붉히면서도, 뚫어지게# 흥미롭게\@ 바라보고 있다……
			ELSE
				PRINTFORML %ARG는%, %CALLNAME:MASTER%들이 뭘 하는지 \@ TALENT:ARG:수치심 > 0 ? 얼굴이 붉히면서 # 흥미진진한 모습으로\@ 듣고 있다
				PRINTFORMW %CALLNAME:MASTER%들은 허둥지둥 %ARG를% 쫓아냈다
				CALL GETOUT(ARG)
				CALL TARGETSET_CHACK
				CFLAG:ARG:동실 = 0
			ENDIF
		;そのキャラの私室でヤってた場合
		ELSEIF CFLAG:ARG:초기위치 == CFLAG:MASTER:현재위치
			PRINTFORML 하필이면 자기 방이 떡집으로 된 %ARG는%, 화가 나서 %CALLNAME:MASTER%들을 쫓아냈다
			PRINTFORMW %CALLNAME:ARG%에게 약점을 잡혔다
			CALL GET_ANGRY(ARG)
			FORCE_STOP = 1
			FORCE_GETOUT = 1
			CFLAG:ARG:약점잡음 += 20
			DRAWLINE
		ELSEIF CFLAG:ARG:행동 == 5 && CFLAG:MASTER:현재위치 == CFLAG:ARG:직장
			PRINTFORML 하필이면 일자리가 떡집으로 된 %ARG는%, 화가 나서 %CALLNAME:MASTER%들을 쫓아냈다
			PRINTFORMW %CALLNAME:ARG%에게 약점을 잡혔다
			CALL GET_ANGRY(ARG)
			FORCE_STOP = 1
			FORCE_GETOUT = 1
			CFLAG:ARG:약점잡음 += 20
			DRAWLINE
		ELSEIF CFLAG:ARG:태도 < 3 && PRIVATEROOM:(CFLAG:MASTER:현재위치 % 100) && PRIVATEROOM:(CFLAG:MASTER:현재위치 % 100) != TARGET
			SIF !CFLAG:ARG:약점잡음
				PRINTFORMW %CALLNAME:ARG%에게 약점을 잡혔다
			IF !NOZOKI
				;見た人
				;CALL KOJO_EVENT(1,ARG,WITNESSED)
				CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,1)
				;見られた人（最も地位の高いTARGETで代表）
				;CALL KOJO_EVENT(2,WITNESSED,ARG)
				CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,1)
			ENDIF
			CALL TOUCH_SET(0,0,0,1)
			DRAWLINE
			FORCE_STOP = 1
			CFLAG:ARG:약점잡음 += 10
			;謹慎処分とか弱味とかの処理を入れる予定
		ELSE
			IF !NOZOKI
				;態度だけだと기정사실とか見てないことに気づく、ので合意がある場合に迫ってくるように
				IF TCVAR:MASTER:친해져 >= 3 && GETBIT(CFLAG:ARG:기정사실 , 1)
					PRINTFORMW %ARG는% 질세라 %CALLNAME:MASTER%에게 몸을 문질렀다
					CFLAG:ARG:우후후 = CFLAG:(WITNESSED):우후후
					;一応、押し倒し可能なムードに
					;理性は押し倒しに関係してないけど、理性的なら混ざろうとしないだろうと
					BASE:ARG:무드 = MAX(BASE:ARG:무드, 300)
					BASE:ARG:이성 = MIN(BASE:ARG:이성, 300)
					;見た人
					CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,2)
					CALL UWAKI_RECORD(ARG,1)
					;見られた人（最も地位の高いTARGETで代表）
					CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,2)
					CALL UWAKI_RECORD(WITNESSED,1)
					DRAWLINE
					RETURN 0
				ELSE
					PRINTFORMW %ARG는% \@TALENT:ARG:연모 ?아연실색하며 #어색한 듯 고개를 돌리고, 허둥지둥 \@ 그 자리를 떠났다…
					;見た人
					CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,3)
					;見られた人（最も地位の高いTARGETで代表）
					CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,3)
					IF CFLAG:ARG:현재위치 == CFLAG:ARG:전턴위치
						CALL GETOUT(ARG)
					ELSE
						CFLAG:ARG:현재위치 = CFLAG:ARG:전턴위치
					ENDIF
					CALL TARGETSET_CHACK
					CFLAG:ARG:동실 = 0
					IF TALENT:ARG:연모
						TALENT:ARG:기분 = MIN(TALENT:ARG:기분, 0)
						BASE:ARG:분노 += MAXBASE:ARG:분노 / 2
						CFLAG:ARG:정사목격 = TARGET
					ENDIF
					RETURN 1
				ENDIF
			;覗かれた場合
			ELSE
				PRINTFORMW %ARG는% \@TALENT:ARG:연모 ?아연실색하고 있다 #어색한 듯 고개를 돌렸다\@
				IF TALENT:ARG:연모 && !CFLAG:ARG:정사목격
					IF TCVAR:ARG:친해져 < 3
						TALENT:ARG:기분 = MIN(TALENT:ARG:기분, 0)
						BASE:ARG:분노 += MAXBASE:ARG:분노 / 4
					ENDIF
					CFLAG:ARG:정사목격 = TARGET
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF
IF FORCE_STOP
	SIF CFLAG:MASTER:장난
		CALL SET_PRANK(0,TARGET,2)
	FOR LOCAL,0,CHARANUM
		CALL ENDUFUFU(LOCAL)
		SIF LOCAL == WITNESSED || LOCAL == ARG
			CONTINUE
		CALL GETOUT(LOCAL)
	NEXT
	TFLAG:106 = 0
	RETURN 0
ENDIF
;通常遭遇
;こちらからも見える位置にいる、もしくは同室
IF !CFLAG:ARG:수면 && (!NOZOKI || CAN_SEE(ARG)) && !FLAG:70 && !CHK_DATENOW(CFLAG:ARG:데이트중) && TIME != TIME:3 && !GETBIT(FLAG:잠복,0)
	;デート中に偶然遭遇
	IF CHK_DATENOW(CFLAG:MASTER:데이트중)
		IF !CFLAG:(NO:ARG):안면 && TALENT:ARG:스침
			PRINTFORMW %마스터는% %CALLNAME:ARG%에게 말을 걸었봤다
		ELSE
			IF !TCVAR:ARG:오늘만났는지
				PRINTFORMW 외출한 곳에서 %ARG와% 만났다
			ELSE
				PRINTFORMW 외출한 곳에서 %ARG와% 또 만났다
			ENDIF
		ENDIF
		IF CFLAG:(NO:ARG):안면 == 0
			CALL SYOTAIMEN(ARG)
		ELSE
			FOR LOCAL:4,1,CHARANUM
				IF CHK_DATENOW(CFLAG:(LOCAL:4):데이트중)
					IF !TCVAR:ARG:오늘만났는지
						CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,9,LOCAL:4)
						PRINTL 
					ENDIF
					CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,7,LOCAL:4)
					BREAK
				ELSEIF LOCAL:4 == CHARANUM - 1
					IF !TCVAR:ARG:오늘만났는지
						CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,9,0)
						PRINTL 
					ENDIF
					CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,6,0)
				ENDIF
			NEXT
		ENDIF
	;さっきから居るのは分かっていたか否か
	ELSEIF CFLAG:ARG:전턴위치 == CFLAG:ARG:현재위치
		PRINTFORM %ARG가% 
		PRINTFORM %NAME_FROM_PLACE(CFLAG:ARG:현재위치)%
		PRINTFORMW 에 있\@CAN_MOVE(CFLAG:MASTER:전턴위치, CFLAG:ARG:현재위치) == 2 ? 다 # 었다\@
		SIF !TCVAR:ARG:오늘만났는지
			PRINTFORML %ARG를% 본 %마스터는%, 오늘은 아직 인사하지 않았다고 생각했다
		LOCAL:3 = 1
	;風呂に入ってきた場合
	ELSEIF CAN_MOVE(CFLAG:MASTER:전턴위치, CFLAG:ARG:전턴위치) < 2 && CFLAG:ARG:전턴위치 != CFLAG:MASTER:전턴위치 && BATHCHECK(CFLAG:MASTER:현재위치)
		PRINTFORMW %ARG가% 목욕탕에 들어왔다…
		IF 목욕탕추방조건(ARG)
			IF CFLAG:ARG:약점잡음 && HETEROSEX(MASTER,ARG)
				PRINTFORMW %마스터는% %CALLNAME:ARG% 반항 못하고 목욕탕에서 쫓겨났다
				CFLAG:MASTER:현재위치 = KICKOUT()
				LOCAL:3 = 5
				CALL TARGETSET_CHACK
				CFLAG:ARG:동실 = 0
			ELSE
				PRINTFORMW %ARG는% %마스터와% 눈이 마주치자 허둥지둥 그 자리를 떠났다…
				CFLAG:ARG:현재위치 = CFLAG:ARG:전턴위치
				CALL TARGETSET_CHACK
				CFLAG:ARG:동실 = 0
				LOCAL:3 = 4
			ENDIF
		ELSE
			PRINTFORMW %ARG는% %마스터와% 함께 들어가고 싶어하는 것 같다
			SIF !TCVAR:ARG:오늘만났는지
				PRINTFORML %마스터는% %CALLNAME:ARG%에게, 오늘은 아직 인사하지 않았다는 것을 깨달았다
			LOCAL:3 = 3
		ENDIF
	;トイレに入ってきた場合
	ELSEIF CAN_MOVE(CFLAG:MASTER:전턴위치, CFLAG:ARG:전턴위치) < 2 && CFLAG:ARG:전턴위치 != CFLAG:MASTER:전턴위치 && IN_TOILET(CFLAG:MASTER:현재위치)
		PRINTW 누군가가 노크를 했다
		$TOILET_INPUT_LOOP
		PRINTL [0]들어오세―요　[1]잘 부탁드립니다
		INPUT
		IF RESULT == 1
			PRINTFORM %ARG가% 
			PRINTFORMS NAME_FROM_PLACE(CFLAG:ARG:현재위치)
			PRINTFORMW 에 들어왔다
			LOCAL:3 = 2
		ELSEIF RESULT == 0
			PRINTW 발소리가 멀어졌다
			CFLAG:ARG:현재위치 = CFLAG:ARG:전턴위치
			CALL TARGETSET_CHACK
			CFLAG:ARG:동실 = 0
			LOCAL:3 = 0
		ELSE
			GOTO TOILET_INPUT_LOOP
		ENDIF
	;ELSEIF CAN_MOVE(CFLAG:MASTER:전턴위치, CFLAG:ARG:전턴위치) < 2 && CFLAG:ARG:전턴위치 != CFLAG:MASTER:전턴위치
	;↑の条件では来訪時に初対面口上が出ない場合があるので変更
	ELSEIF CFLAG:ARG:전턴위치 != CFLAG:MASTER:전턴위치
		IF CFLAG:ARG:전턴위치 != MAXROOM && CFLAG:ARG:전턴위치 != OMANEKIBEYA()
			PRINTFORMW %ARG가% %NAME_FROM_PLACE(CFLAG:ARG:현재위치)%에 왔다
		ENDIF
		SIF !TCVAR:ARG:오늘만났는지
			PRINTFORML %마스터는% %CALLNAME:ARG%에게, 오늘은 아직 인사하지 않았냐고 되물었다
		LOCAL:3 = 2
	ENDIF
	IF !FLAG:70 && CFLAG:ARG:현재위치 == CFLAG:MASTER:현재위치
		IF !NOZOKI && LOCAL:3
			;初対面
			IF CFLAG:(NO:ARG):안면 == 0
				CALL SYOTAIMEN(ARG)
			ELSEIF CFLAG:ARG:정사목격 && CFLAG:(CFLAG:ARG:정사목격):현재위치 != CFLAG:ARG:현재위치
				PRINTFORMW %CALLNAME:ARG%에게 전의 %조사처리(CALLNAME:(CFLAG:ARG:정사목격),"와")%의 일에 대해서 추궁당했다…
				;CALL KOJO_EVENT(0, ARG, 2)
				CALL KOJO_MESSAGE_SEND("EVENT",8,ARG,1,0)
				CFLAG:ARG:정사목격 = 0
			ELSEIF CFLAG:ARG:정사목격 && CFLAG:(CFLAG:ARG:정사목격):현재위치 == CFLAG:ARG:현재위치
				PRINTFORMW %ARG는% 전의 일에 대해 %마스터와% %CALLNAME:(CFLAG:ARG:정사목격)%에게 설교했다…
				CALL KOJO_MESSAGE_SEND("EVENT",8,ARG,2,0)
				CFLAG:ARG:정사목격 = 0
			ELSE
				IF !TCVAR:ARG:오늘만났는지
					CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,8,0)
					PRINTL 
				ENDIF
				;通常遭遇口上
				CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,LOCAL:3,0)
			ENDIF
		ENDIF
		CALL FirstMeetToday(ARG)
	ENDIF
ENDIF

@OSASOI(ARG)
#DIM 목적지
#DIM 자택지역
#DIM 현재지역

자택지역 = CFLAG:ARG:자택위치 / 100
현재지역 = GET_MAPID(CFLAG:MASTER:현재위치)
;仕事がある日は誘わない
SIF !CHARA_HOLIDAY(ARG)
	RETURN
SIF GROUPMATCH(TIME:5, 5, 9)
	RETURN
SIF !AT_HOME(MASTER)
	RETURN
IF (TALENT:ARG:연모 || TALENT:ARG:사모) && CFLAG:ARG:연회참가 && TFLAG:연회장 / 100 != MAIN_MAP && TFLAG:연회장 / 100 != 현재지역 && (FLAG:개시일 - DAY) == 0
	목적지 = TFLAG:연회장 / 100
	PRINTFORML %CALLNAME:ARG%에게서 오늘 연회가 있는 %GET_MAPNAME(목적지)%에 가자고 권유됐다
	GOTO INVITE_ENKAI
ENDIF
IF TALENT:ARG:연모 && !RAND:5 && !CFLAG:ARG:연회참가
	IF CFLAG:ARG:욕구불만도 >= (700 + TALENT:ARG:수치심 * 150) && !CFLAG:ARG:신사거주 && !CFLAG:ARG:자택위치
		PRINTFORML %ARG가% 방으로 오라고 했다
		CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,3,자택지역)
		CALL ASK_YN("간다","그만둔다")
		IF !RESULT
			PRINTFORML %CALLNAME:ARG%의 권유로, %마스터는% 그녀의 방으로 향했다……
			CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,4,자택지역)
			PRINTFORMW +{TIME_REQUIRED(현재지역, 자택지역)}분
			TIME += TIME_REQUIRED(현재지역, 자택지역)
			CFLAG:ARG:데이트중 = 자택지역
			CFLAG:MASTER:데이트중 = 자택지역
			FLAG:데이트상대 = ARG
			CFLAG:MASTER:동행 = 60
			CFLAG:ARG:동행 = 60
			TFLAG:데이트전호감도 = CFLAG:ARG:2
			TARGET = ARG
			CALL OMANEKI_ENTER(ARG)
			CALL TARGETSET_CHACK
			;お誘いに乗ったか断ったかをRETURNで返すようにしました
			RETURN 1
		ELSE
			PRINTFORMW %ARG는% 그렇게 말해서 그런지 유감스러운 것 같다
			CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,5,자택지역)
			RETURN 0
		ENDIF
	ELSEIF !RAND:4 && CFLAG:ARG:자주가는지역 >= 0 && CFLAG:ARG:자주가는지역 != MAIN_MAP
		목적지 = CFLAG:ARG:자주가는지역
	ELSE
		목적지 = RAND:10
		SIF 목적지 == MAIN_MAP
			RETURN
	ENDIF
	;要求移動時間が60以上だと不発
	IF TIME_REQUIRED(현재지역, 목적지) >= 60
		RETURN 0
	ENDIF
	PRINTFORML %CALLNAME:ARG%에게서 %GET_MAPNAME(목적지)%에 가자고 권유됐다
	$INVITE_ENKAI
	CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,0,목적지)
	CALL ASK_YN("간다","그만둔다")
	IF !RESULT
		CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,1,목적지)
		CFLAG:MASTER:동행 = 60
		CFLAG:ARG:동행 = 60
		TFLAG:데이트전호감도 = CFLAG:ARG:2
		CALL SET_DATE(목적지,ARG)
		CALL TARGETSET_CHACK
		RETURN 1
	ELSE
		PRINTFORMW %ARG는% 아쉬운 것 같다
		CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,2,목적지)
		RETURN 0
	ENDIF
ENDIF

RETURN 0



@UWAKI_RECORD(ARG,공인,바람상대)
#DIM 바람상대
#DIM 공인

SIF UWAKI_KNOWN:ARG:0 == ""
	UWAKI_KNOWN:ARG:0 = /
SIF UWAKI_ACCEPTED:ARG:0 == ""
	UWAKI_ACCEPTED:ARG:0 = /


IF 바람상대
	LOCALS = %CALLNAME:바람상대%
	SIF !STRCOUNT(UWAKI_KNOWN:ARG:0,LOCALS)
		UWAKI_KNOWN:ARG:0 = %UWAKI_KNOWN:ARG:0%%LOCALS%/
	SIF !STRCOUNT(UWAKI_ACCEPTED:ARG:0,LOCALS) && 공인
		UWAKI_ACCEPTED:ARG:0 = %UWAKI_ACCEPTED:ARG:0%%LOCALS%/
ELSE
	;第三引数が0の場合、あなたと同じ場所にいるキャラ全員記録
	FOR LOCAL,1,CHARANUM
		SIF LOCAL == ARG
			CONTINUE
		SIF CFLAG:LOCAL:현재위치 != CFLAG:MASTER:현재위치
			CONTINUE
		;迫る予定の無い人を弾く応急処置。
		SIF !(TCVAR:MASTER:친해져 >= 3 && GETBIT(CFLAG:LOCAL:기정사실, 1))
			CONTINUE
		LOCALS = %CALLNAME:LOCAL%
		SIF !STRCOUNT(UWAKI_KNOWN:ARG:0,LOCALS)
			UWAKI_KNOWN:ARG:0 = %UWAKI_KNOWN:ARG:0%%LOCALS%/
		SIF !STRCOUNT(UWAKI_ACCEPTED:ARG:0,LOCALS) && 공인
			UWAKI_ACCEPTED:ARG:0 = %UWAKI_ACCEPTED:ARG:0%%LOCALS%/
	NEXT
ENDIF

@GET_UWAKI_HISTORY(ARG,바람상대)
;公認だと2を返す
#FUNCTION
#DIM 바람상대
LOCALS = /%CALLNAME:바람상대%/

IF STRCOUNT(UWAKI_ACCEPTED:ARG:0,LOCALS)
	RETURNF 2
ELSEIF STRCOUNT(UWAKI_KNOWN:ARG:0,LOCALS)
	RETURNF 1
ELSE
	RETURNF 0
ENDIF

@SYOTAIMEN(ARG)
SIF CFLAG:ARG:안면
	RETURN
IF TALENT:ARG:스침 == 1
	IF TALENT:MASTER:매혹
		PRINTFORMW %ARG는% %CALLNAME:MASTER%에게 【매혹】당했다
		CALL COLORMESSAGE(@"好感度と親密が上がった",C_YELLOW,2,1)
		ABL:ARG:친밀 += 2
		CFLAG:ARG:호감도 += 300
	ENDIF
	IF TALENT:MASTER:수수께끼의매력
		PRINTFORMW %ARG는% %CALLNAME:MASTER%의 【수수께끼의매력】에 가슴이 크게 뛰는 것을 느꼈다
		CALL COLORMESSAGE(@"好感度と親密が上がった",C_YELLOW,2,1)
		ABL:ARG:친밀 += 2
		CFLAG:ARG:호감도 += 300
	ENDIF
ENDIF
IF !FLAG:역극
	SETCOLOR 0xc53d43
	SIF NO:ARG != 거점_주인
		PRINTFORMW %ARG는% 처음 만난 %CALLNAME:MASTER%에게 인사했다
	RESETCOLOR
	CALL KOJO_MESSAGE_SEND("ENCOUNTER",0,ARG,1,0)
ENDIF
CFLAG:ARG:안면 = 1

@FirstMeetToday(ARG)
#DIM PREV_TARGET
#DIM IRAI_ABLE
SIF CFLAG:ARG:현재위치 != CFLAG:MASTER:현재위치
	RETURN
SIF TCVAR:ARG:오늘만났는지
	RETURN

TCVAR:ARG:오늘만났는지 = 1
CFLAG:ARG:마지막에만난날 = DAY
SIF TCVAR:ARG:어나더_평상복
	PRINTFORMW 오늘 %ARG는% 평소와 다른 모습을 하고 있다
IF CFLAG:ARG:딸딸이들킴
	;陥落素質あり、【快感に素直】の時、わりと素直な反応に
	IF CFLAG:ARG:딸딸이들킴 == 1
		IF (IS_LOVER(ARG) || TALENT:ARG:섹프) && TALENT:ARG:쾌감응답 == 1
			PRINTFORMW 요전 자위 현장을 목격당한 %ARG는%, %TEXTR("시치미를 떼고 있다…/뭔가 말하고 싶은 듯한 얼굴로 이쪽을 보고 있다…/애써 태연해 하고 있다…")%
		ELSE
			PRINTFORMW 요전 자위 현장을 목격당한 %ARG는% 어색한 듯하다…
		ENDIF
	ELSEIF CFLAG:ARG:딸딸이들킴 == 2
		IF (IS_LOVER(ARG) || TALENT:ARG:섹프) && TALENT:ARG:쾌감응답 == 1
			PRINTFORMW 요전 자위 현장을 덮쳐졌을 때, 내친 김에 하게해 준 %ARG가% %TEXTR("요염하게 곁눈질을 하고 있다…/이쪽을 보고 히죽히죽거리고 있다…/좋은 미소를 띠고 있다…")%
		ELSE
			PRINTFORMW 요전 자위 현장을 목격당하고, 뜻밖에도 몸까지 허락하고 만 %ARG는% 어색한 듯하다…
		ENDIF
	ELSEIF CFLAG:ARG:딸딸이들킴 == 3
		IF (IS_LOVER(ARG) || TALENT:ARG:섹프) && TALENT:ARG:쾌감응답 == 1
			PRINTFORMW 요전 자위 현장을 덮쳐졌을 때, 성대한 절정을 보여준 %ARG는% %TEXTR("이쪽으로 윙크를 한다…/이쪽으로 눈을 깜빡깜빡거리고 있다…/조금 쑥스러운 듯 하다…")%
		ELSE
			PRINTFORMW 요전 자위 현장을 목격당했을 뿐만 아니라, 눈앞에서 성대하게 가버리는 것까지 보여주고 만 %ARG는% 어색한 듯하다…
		ENDIF
	ENDIF
	CFLAG:ARG:딸딸이들킴 = 0
ELSEIF TALENT:ARG:기분 == 1
	PRINTFORMW 무슨 좋은 일이라도 있었는지, 오늘 %ARG는% 매우 기분이 좋다
ELSEIF TALENT:ARG:기분 == -1
	PRINTFORMW 아무래도 오늘 %ARG는% 기분이 언짢은 것 같다…
ENDIF
IF CFLAG:ARG:욕구불만도 >= 500 && TALENT:MASTER:정욕감지
	SETCOLOR 255,100,255
	FONTBOLD
	PRINTFORMW 피키―인!
	PRINTFORMW %CALLNAME:MASTER%에게는 %ARG가% 성욕을 주체하지 못하고 있다는 것을 알 수 있다……!
	RESETCOLOR
	FONTREGULAR
ENDIF
CALL SEASONAL(ARG)

PREV_TARGET = TARGET
TARGET = ARG
CALL CAN_IRAI_START
IRAI_ABLE = RESULT
IF IRAI_ABLE && IRAI_CHARA_TO_PROGRESS(ARG) == "의뢰미수탁" && !FLAG:의뢰패스
	;CALL先の判定でTARGET==CLIENT前提の処理が多々あるので一時変更、そのまま戻ってくるので退避しとく
	CALL IRAI_START(ARG)
	TARGET = PREV_TARGET
ELSEIF IRAI_ABLE && GROUPMATCH(IRAI_CHARA_TO_PROGRESS(ARG), "기한 마감", "보고 잊음")
	PRINTFORML %ARG는% %마스터가% 의뢰를 달성하지 않은 것을 책망하고 있다
	;TFLAG:104の使い方がよくわからん
	;TFLAG:104 = ARG
	CALL IRAI_REPORT(ARG)
	TARGET = PREV_TARGET
ELSEIF TALENT:ARG:기분 >= 0 && BASE:MASTER:기력 > 0 && BASE:ARG:기력 > 0 && !CFLAG:MASTER:동행 && NEMUKE() < 720 && !수면시간(ARG)
	CALL OSASOI(ARG)
	SIF !RESULT
		TARGET = PREV_TARGET
ELSE
	TARGET = PREV_TARGET
ENDIF