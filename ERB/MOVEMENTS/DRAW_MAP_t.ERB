;-----------------------------------------------------------
;맵 묘화 처리
;2캐릭터 세트의 반각 숫자(01이라든지)는 자동으로 버튼화, 전각 숫자는 인식하지 않는다
;-----------------------------------------------------------
@DRAW_MAP(ARGS)
#DIMS AA, 40
#DIM SPOT
#DIM MODE
#DIM LOCATION

#DIM LINENUM
#DIM X_POINT
#DIM FOUND_INT

;AA를 취득, 없으면 그대로 돌아간다
VARSET AA
;외출 맵인가 어떤가의 분기
IF ARGS == "ODEKAKE"
	TRYCCALLFORM ODEKAKEMAP_{CFLAG:MASTER:데이트중}(AA)
	CATCH
		RETURN 0
	ENDCATCH
ELSE
	TRYCCALLFORM MAP_VIEWING_{MAIN_MAP}(AA)
	CATCH
		RETURN 0
	ENDCATCH
ENDIF	

;-----------------------------------------------------------
;묘화 처리
;-----------------------------------------------------------
FOR LINENUM, 0, VARSIZE("AA")
	SIF AA:LINENUM == ""
		CONTINUE
	;행 마다 초기화
	X_POINT = 0
	RESULTS = I
	;행의 종단까지 루프
	WHILE RESULTS != "" && STRLENSU(RESULTS) < 3
		;1문자 꺼낸다
		SUBSTRINGU AA:LINENUM, X_POINT, 1
		;숫자라면 버튼으로 한다
		IF ISNUMERIC(RESULTS)
			;분기 귀찮기 때문에 반드시 2자릿수 취한다
			SUBSTRINGU AA:LINENUM, X_POINT, 2
			FOUND_INT = TOINT(RESULTS)
			;외출 맵인가 어떤가의 분기
			IF ARGS == "ODEKAKE"
				SPOT = FOUND_INT
				CALL GETMAP2(SPOT)
			ELSE
				SPOT = MAIN_MAP * 100 + FOUND_INT
				CALL GETMAP(SPOT)
			ENDIF
			X_POINT += 2
		;기호에 의미를 갖게하는 예, 이것이라면#은 보통으로는 사용할 수 없게 되지만 뭐 좋은가
		ELSEIF RESULTS == "#"
			;반드시 2 문자 잡는다
			SUBSTRINGU AA:LINENUM, X_POINT, 2
				IF ARGS == "HIKKOSHI"
					TRYCALLFORM DRAW_MAP_BG_{MAIN_MAP}(RESULTS)
				ELSE
					TRYCALLFORM DRAW_MAP_BG_{CFLAG:MASTER:현재위치 / 100}(RESULTS)
				ENDIF
			X_POINT += 2
		;보통의 문자
		ELSE
			;CALL DRAW_MAP_BG(RESULTS)
			IF ARGS == "HIKKOSHI"
				TRYCALLFORM DRAW_MAP_BG_{MAIN_MAP}(RESULTS)
			ELSE
				TRYCALLFORM DRAW_MAP_BG_{CFLAG:MASTER:현재위치 / 100}(RESULTS)
			ENDIF
			X_POINT ++
		ENDIF
	WEND
	PRINTL
NEXT

RESETCOLOR


;===========================================================
;장소명 표시
;===========================================================
@PRINT_SPOT
#DIMS SPOTNAME, 100

VARSET SPOTNAME
CALLFORM MAP_SPOTNAME_{MAIN_MAP}(SPOTNAME)
FOR  LOCAL, 0, VARSIZE("SPOTNAME")
	IF SPOTNAME:LOCAL != ""
		IF MAIN_MAP * 100 + LOCAL != CFLAG:MASTER:현재위치
			PRINTBUTTON TOSTR(LOCAL, "00") + "." + SPOTNAME:LOCAL + "　", MAIN_MAP * 100 + LOCAL
		ELSE
			SETCOLOR 0x00FF00
			PRINTS TOSTR(LOCAL, "00") + "." + SPOTNAME:LOCAL + "　"
			RESETCOLOR
		ENDIF
	ENDIF
NEXT
