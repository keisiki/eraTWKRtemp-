@SKIP_MOVE_6(ARG:0, ARG:1)
#DIM 현재의위치
#DIM CONST SPT중유의길 = 601
#DIM CONST SPT삼도천모래밭 = 602
#DIM CONST 돌총 = 603
#DIM CONST SPT피안 = 604

#DIM CONST 우루미집 = 608
#DIM CONST 에이카집 = 609
#DIM CONST 시비곡직청홀 = 610

#DIM CONST 법정 = 612
#DIM CONST 시비곡직청복도 = 613
#DIM CONST 시비곡직청휴게실 = 614
#DIM CONST 에이키의방 = 615
#DIM CONST 코마치의방 = 616
#DIM CONST 시비곡직청목욕탕 = 617
#DIM CONST 시비곡직청빈방 = 618

#DIM CONST 명계의문 = 620
#DIM CONST SPT백옥루정원 = 621
#DIM CONST 백옥루복도 = 622
#DIM CONST 유우코방 = 623
#DIM CONST 요우무방 = 624
#DIM CONST 아나타방= 625
#DIM CONST 백옥루부엌 = 626
#DIM CONST 백옥루목욕탕 = 627
#DIM CONST 백옥루거실 = 628
#DIM CONST 사이교 = 629
#DIM CONST 야쿠모가현관 = 630
#DIM CONST 야쿠모가복도 = 631
#DIM CONST 야쿠모가정원 = 632
#DIM CONST 유카리방 = 633
#DIM CONST 야쿠모가빈방 = 634
#DIM CONST 첸의방 = 635
#DIM CONST 야쿠모가목욕탕 = 636
#DIM CONST 야쿠모가부엌 = 637
#DIM CONST 란의방 = 638
#DIM CONST 야쿠모가거실 = 639
#DIM CONST 이계의문 = 640
#DIM CONST 축생메트로폴리스 = 641
#DIM CONST SPT영장원 = 642
#DIM CONST 영장원목욕탕 = 643
#DIM CONST 케이키의방 = 644
#DIM CONST 마유미의방 = 645
#DIM CONST 야치에의목욕탕 = 646
#DIM CONST 야치에의공간 = 647
#DIM CONST 야치에의침실 = 648

#DIM CONST 사키의공간 = 650
#DIM CONST 사키의침실 = 651
#DIM CONST 사키의목욕탕 = 652
;현재 위치 취득
현재의위치 = CFLAG:MASTER:현재위치
SIF ARG:1
	현재의위치 = ARG:1

;暫定目的地に直接移動できる場合
IF CAN_MOVE(현재의위치,ARG) & 1
	;そこに移動
	RETURN ARG
ENDIF

;各エリアから外部へ行く場合
;是非曲直庁から外部
IF INRANGE(현재의위치, 시비곡직청홀, 619) && !INRANGE(ARG, 시비곡직청홀, 619)
	RETURN 시비곡직청홀
ENDIF
;白玉楼から外部
IF INRANGE(현재의위치, SPT백옥루정원, 사이교) && !INRANGE(ARG, 명계의문, 사이교)
	RETURN 명계의문
ENDIF
;八雲邸から外部
IF INRANGE(현재의위치, 야쿠모가복도, 야쿠모가거실) && !INRANGE(ARG, 야쿠모가현관, 야쿠모가거실)
	RETURN 야쿠모가현관
ENDIF
;畜生界から外部
IF INRANGE(현재의위치, 축생메트로폴리스, 사키의목욕탕) && !INRANGE(ARG, 이계의문, 사키의목욕탕)
	RETURN 이계의문
ENDIF

;袋小路から行ける場所は固定なので、そこを目指す
SELECTCASE 현재의위치
CASE 우루미집
	RETURN SPT삼도천모래밭
CASE 에이카집
	RETURN 돌총
CASE 에이키의방
	RETURN 시비곡직청휴게실
CASE 코마치의방, 시비곡직청목욕탕, 시비곡직청빈방
	RETURN 시비곡직청복도
CASE 요우무방, 아나타방, 백옥루목욕탕
	RETURN 백옥루복도
CASE 야쿠모가목욕탕, 야쿠모가부엌
	RETURN 야쿠모가복도
CASE 영장원목욕탕, 케이키의방, 마유미의방
	RETURN SPT영장원
CASE 야치에의목욕탕, 야치에의침실
	RETURN 야치에의공간
CASE 사키의침실, 사키의목욕탕
	RETURN 사키의공간
ENDSELECT

;直接移動できない場合、1つ近い位置を暫定目的地に設定し直してループ
;地点追加時要フォロー（全ケース修正すること）
SELECTCASE ARG
CASE SPT중유의길
	RETURN SPT삼도천모래밭
CASE SPT삼도천모래밭
	RETURN SPT피안
CASE 돌총
	SELECTCASE 현재의위치
	CASE SPT중유의길
		RETURN SPT삼도천모래밭
	CASEELSE
		RETURN SPT피안
	ENDSELECT
CASE SPT피안
	RETURN SPT삼도천모래밭
CASE 우루미집
	RETURN SPT삼도천모래밭
CASE 에이카집
	RETURN 돌총

;是非曲直庁は欠番の611があるので一応残しておく
CASE 시비곡직청홀
	SELECTCASE 현재의위치
	CASE 611 TO 619
		RETURN 시비곡직청복도
	CASEELSE
		RETURN SPT피안
	ENDSELECT
CASE 법정
	SELECTCASE 현재의위치
	CASE 시비곡직청홀 TO 619
		RETURN 시비곡직청복도
	CASEELSE
		RETURN 시비곡직청홀
	ENDSELECT
CASE 시비곡직청복도
	RETURN 시비곡직청홀
CASE 시비곡직청휴게실, 코마치의방, 시비곡직청목욕탕, 시비곡직청빈방
	RETURN 시비곡직청복도
CASE 에이키의방
	RETURN 시비곡직청휴게실

CASE 명계의문
	SELECTCASE 현재의위치
	CASE 백옥루복도 TO 사이교
		RETURN SPT백옥루정원
	CASEELSE
		RETURN SPT피안
	ENDSELECT
CASE SPT백옥루정원
	SELECTCASE 현재의위치
	CASE 유우코방 TO 백옥루목욕탕
		RETURN 백옥루복도
	CASEELSE
		RETURN 명계의문
	ENDSELECT
CASE 백옥루복도
	RETURN SPT백옥루정원
CASE 유우코방, 요우무방, 아나타방, 백옥루부엌, 백옥루목욕탕
	RETURN 백옥루복도
CASE 백옥루거실
	SELECTCASE 현재의위치
	CASE 요우무방, 아나타방, 백옥루목욕탕
		RETURN 백옥루복도
	CASEELSE
		RETURN SPT백옥루정원
	ENDSELECT
CASE 사이교
	RETURN SPT백옥루정원

CASE 야쿠모가현관
	SELECTCASE 현재의위치
	CASE 유카리방 TO 야쿠모가거실
		RETURN 야쿠모가복도
	CASEELSE
		RETURN SPT삼도천모래밭
	ENDSELECT
CASE 야쿠모가복도, 야쿠모가정원
	RETURN 야쿠모가현관
CASE 유카리방, 야쿠모가목욕탕, 야쿠모가부엌, 란의방, 야쿠모가거실
	RETURN 야쿠모가복도
CASE 야쿠모가빈방, 첸의방
	SELECTCASE 현재의위치
	CASE 유카리방 TO 야쿠모가거실
		RETURN 야쿠모가복도
	CASEELSE
		RETURN 야쿠모가현관
	ENDSELECT

CASE 이계의문
	SELECTCASE 현재의위치
	CASE SPT영장원 TO 사키의목욕탕
		RETURN 축생메트로폴리스
	CASEELSE
		RETURN SPT피안
	ENDSELECT
CASE 축생메트로폴리스
	SELECTCASE 현재의위치
	CASE 영장원목욕탕, 케이키의방, 마유미의방
		RETURN SPT영장원
	CASE 야치에의목욕탕, 야치에의침실
		RETURN 야치에의공간
	CASE 사키의침실, 사키의목욕탕
		RETURN 사키의공간
	CASEELSE
		RETURN 이계의문
	ENDSELECT
CASE SPT영장원, 야치에의공간, 사키의공간
	RETURN 축생메트로폴리스
CASE 영장원목욕탕, 케이키의방, 마유미의방
	RETURN SPT영장원
CASE 야치에의목욕탕, 야치에의침실
	RETURN 야치에의공간
CASE  사키의침실, 사키의목욕탕
	RETURN 사키의공간

CASEELSE
	RETURN SPT중유의길
ENDSELECT
