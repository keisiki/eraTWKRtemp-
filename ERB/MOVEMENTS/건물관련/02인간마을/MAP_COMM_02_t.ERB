;--------------------------------------------------
;	맵내 이동 처리 확장 패치 부속
;	명련사MAP용 함수
;--------------------------------------------------

@MAP_PLACENAME_2(ARG)
#FUNCTIONS
#DIM PLACEID

IF GET_MAPID(ARG) == MAIN_MAP
	SELECTCASE ARG
		CASE 201 TO (MAXROOM - 1)
			RETURNF STR:(ARG + 8000)
		CASE 298
			RETURNF OMANEKI_PLACE()
		CASE 299
			RETURNF "스키마 공간"
		CASE MAXROOM
			RETURNF "길의 저 편"
		CASEELSE
			RETURNF "불명"
	ENDSELECT
ELSE
	RETURNF "불명"
ENDIF

@MAP_VIEWING_2(AA,COLOR)
#DIMS REF AA, 0
#DIMS REF COLOR, 0
IF !TFLAG:맵변환
	SELECTCASE CFLAG:MASTER:현재위치
		CASE 214, 230 TO 237, 208 TO 210, 241
			TFLAG:맵변환 = 2
		CASE 213, 226
			TFLAG:맵변환 = 3
		CASE 238, 239
			TFLAG:맵변환 = 4
		CASEELSE
			TFLAG:맵변환 = 1
	ENDSELECT
ENDIF
CALL DRAW_INFORMATIONLINE(@"%MAPNAME:(TFLAG:맵변환)%")
PRINTFORML 
IF RECOLOREDMAPS
TRYCCALLFORM COLOREDMAP_SATO_{TFLAG:맵변환}(AA,COLOR)
CATCH
RECOLOREDMAPS = 0
CALL DRAW_MAP
RECOLOREDMAPS = 1
RETURN
ENDCATCH
ELSE
TRYCALLFORM MAP_SATO_{TFLAG:맵변환}(AA)
ENDIF

@MAP_CAN_MOVE_2(ARG, ARG:1)
#FUNCTION
#DIM 재택
SELECTCASE ARG
	;코스즈의 방
	CASE 226
		IF ARG:1 == MASTER
			IF FLAG:70
				RETURNF 1
			ELSEIF CFLAG:[[코스즈]]:동행 || CFLAG:[[아큐]]:동행
				RETURNF 1
			ELSEIF TALENT:[[코스즈]]:연모
				RETURNF 1
			ELSEIF TALENT:[[코스즈]]:사모 && CFLAG:[[코스즈]]:현재위치 == ARG
				RETURNF 1
			ELSE
				TFLAG:이동불능메시지 = ARG
				RETURNF 0
			ENDIF
		;본인은 들어갈 수 있는, 아큐도 넣는다
		ELSEIF ARG:1 == [[코스즈]] || ARG:1 == [[아큐]]
			RETURNF 1
		ELSE
			RETURNF 0
		ENDIF
	;아큐 방
	CASE 229
		IF ARG:1 == MASTER
			IF FLAG:70
				RETURNF 1
			ELSEIF CFLAG:[[아큐]]:동행
				RETURNF 1
			ELSEIF TALENT:[[아큐]]:연모
				RETURNF 1
			ELSEIF TALENT:[[아큐]]:사모 && CFLAG:[[아큐]]:현재위치 == ARG
				RETURNF 1
			ELSE
				TFLAG:이동불능메시지 = ARG
				RETURNF 0
			ENDIF
		;본인은 들어갈 수 있는, 궁합이 좋은 캐릭터도 넣는다
		ELSEIF ARG:1 == [[아큐]] || RELATION:[[아큐]]:(ARG:1) >= 100
			RETURNF 1
		ELSE
			RETURNF 0
		ENDIF
	;미요이 방
	CASE 239
		IF ARG:1 == MASTER
			IF FLAG:70
				RETURNF 1
			ELSEIF CFLAG:[[미요이]]:동행
				RETURNF 1
			ELSEIF TALENT:[[미요이]]:연모
				RETURNF 1
			ELSEIF TALENT:[[미요이]]:사모 && CFLAG:[[미요이]]:현재위치 == ARG
				RETURNF 1
			ELSE
				TFLAG:이동불능메시지 = ARG
				RETURNF 0
			ENDIF
		;본인은 들어갈 수 있는
		ELSEIF ARG:1 == [[미요이]]
			RETURNF 1
		ELSE
			RETURNF 0
		ENDIF
	;노와 혼숙여관, 당신 이외는 들어가지 않는다
	CASE 220, 225
		SIF ARG:1 != MASTER
			RETURNF 0
	;대절욕탕, 당신 이외에는 들어가지 않는다, 대절하지 않으면 들어갈 수 없다
	CASE 228
		SIF ARG:1 != MASTER
			RETURNF 0
		IF !욕탕대절(DAY)
			TFLAG:이동불능메시지 = ARG
			RETURNF 0
		ENDIF
	CASEELSE
		RETURNF 1
ENDSELECT
RETURNF 1


@MAP_CANNOT_WORD_2(ARG)
SELECTCASE TFLAG:이동불능메시지
	CASE 224,231 TO 235, 208 TO 210, 241
		PRINTFORMW 자물쇠가 걸려 있다
	CASE 226, 229, 239
		PRINTFORMW 마음대로 들어가는 것은 곤란해
	CASE 228
		PRINTFORMW 직접 들어가려면 대절하지 않으면 안 된다
ENDSELECT

;유사 TFLAG로 취급한다, 날짜가 바뀌면 종료
;ARG = DAY
@욕탕대절(ARG, 전세내다)
#FUNCTION
#DIM 대절일
#DIM 전세내다
IF 전세내다
	대절일 = ARG
	RETURNF 1
ELSEIF 대절일 == ARG
	RETURNF 1
ENDIF
RETURNF 0

@MAP소개_2
;とりあえず5行
PRINTFORML 만화등으로 친숙한 인간 마을.
PRINTFORML 다른 지역에 접근성이 좋고, 스즈나암 등 각종 시설이 잘 갖춰져 있어 업무차 정기적으로 내방하는 캐릭터도 많지만,
PRINTFORML 탄막 승부나 텃밭이 안 되고, 채집 장소가 존재하지 않는다는 단점이 있다.
PRINTFORML 요괴의 산 꼭대기로 가는 삭도가 있다.
PRINTFORML 
