@EXIST_FreeAct11
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
@FreeAct11(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
#DIM 경과시간
경과시간 = Activity_Duration:TARGET
SELECTCASE O_DATA
;自由行動に付き合うコマンドの名称
CASE "명칭"
	CALLF MAKE_STR(V_NAME, "실험을 돕기")
CASE "CHAIN"
;他キャラに連鎖しない場合コメントアウトすること
	CALLF MAKE_INT(V_NAME, 1)
CASE "경과시간"
;自由行動に付き合うコマンドでの경과시간
;0だと30分か残り時間すべての小さい方
	CALLF MAKE_INT(V_NAME, 0)
ENDSELECT

@자유행동내용_11(CHARA, ARGS, 시간계수, CHAIN)
#DIM CHARA
#DIM CHAIN
#DIM 현위치
#DIM 메시지표시
#DIM 시간계수
#DIM 경과시간
현위치 = CFLAG:CHARA:현재위치
메시지표시 = 현위치 == CFLAG:MASTER:현재위치 ? 1 # 0
SELECTCASE ARGS
	CASE "발생"
		;조제기능1以上のキャラが調合できる場所（あなた私室以外）または自室にいる時
		SIF ABL:CHARA:조제기능 < 1
			RETURN 0
		SIF BASE:CHARA:기력 <= 500
			RETURN 0
		;連鎖時はスキップ
		IF !CHAIN
			SIF PHARMACY_SPOT(현위치) != 2 && 현위치 != CFLAG:CHARA:신사거주
				RETURN 0
		ENDIF
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% \@ CHAIN ? %조사처리(CALLNAME:CHAIN,"을")% 돕기 시작했다.# 실험 준비를 시작했다.\@
		RETURN 60 + RAND:30
	CASE "행동중"
		IF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% %TEXTR("実験器具を調節している/実験道具を並べている/実験の記録を付けている/수상한 실험을 하고 있다")%.
		ENDIF
	CASE "완료"
		EXP:CHARA:조제경험 += 1
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 실험을 마친 듯 하다.
	CASE "중단"
		EXP:CHARA:조제경험 += 1
	CASE "같이한다"
		;自由行動に付き合ったとき用メッセージ
		PRINTFORML %조교자는% %타겟의% 실험을 도왔다
		SELECTCASE RAND:3
		CASE 1
			PRINTFORML %타겟이% 무엇을 하고 싶은지 잘 모르겠다…
		CASE 2
			PRINTFORML 위험물을 다루었기 때문에 신경이 쓰였다…
		CASEELSE
			PRINTFORML %타겟은% 만족한 표정을 짓고 있다… 
		ENDSELECT
	CASE "소스"
		;付き合うコマンド選択時の入手ソース、시간계수は付き合った時間の1/10
		DOWNBASE:MASTER:체력 += 20 * 시간계수
		DOWNBASE:MASTER:기력 += 40 * 시간계수
		DOWNBASE:TARGET:체력 += 10 * 시간계수
		DOWNBASE:TARGET:기력 += 20 * 시간계수
		SOURCE:환락 = (150 + ((ABL:친밀 + ABL:MASTER:조제기능) * 50)) * 시간계수
		SOURCE:수동 = (100 + ((ABL:순종 + ABL:MASTER:조제기능) * 50)) * 시간계수
		TFLAG:98 = 1
		EXP:MASTER:조제경험 ++
ENDSELECT