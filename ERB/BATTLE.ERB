;--------------------------------------------
@BATTLE_SYSTEM(ARG)
;--------------------------------------------
#DIM 기초공격력
#DIM 전회행동
#DIM 웨이트
#DIMS 바색
ENEMY_NUM = ARG
CALL SET_ENEMY
기초공격력 = ABL:MASTER:전투능력 + 1

$TURN_START
웨이트 = 0
LOCAL = 100 * BASE:MASTER:체력 / MAXBASE:MASTER:체력

LOCALS = 체력(%CALLNAME:MASTER%)
PRINTFORM %LOCALS, 20, LEFT%
SELECTCASE LOCAL
	CASE IS > 50
		CALL PRINT_COLORBAR, BASE:MASTER:0, MAXBASE:MASTER:0, 18, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("파랑")
	CASE 50 TO 20
		CALL PRINT_COLORBAR, BASE:MASTER:0, MAXBASE:MASTER:0, 18, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("노랑")
	CASEELSE
		CALL PRINT_COLORBAR, BASE:MASTER:0, MAXBASE:MASTER:0, 18, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("빨강")
ENDSELECT

PRINTFORM  ({BASE:MASTER:체력, 5}/{MAXBASE:MASTER:체력, 5})
PRINTL
LOCAL = 100 * ENEMY_HP / ENEMY_MAXHP

LOCALS = 체력(%ENEMY_NAME%)
PRINTFORM %LOCALS, 20, LEFT%
SELECTCASE LOCAL
	CASE IS > 50
CALL PRINT_COLORBAR, ENEMY_HP, ENEMY_MAXHP, 18, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("파랑")
	CASE 50 TO 20
CALL PRINT_COLORBAR, ENEMY_HP, ENEMY_MAXHP, 18, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("노랑")
	CASEELSE
CALL PRINT_COLORBAR, ENEMY_HP, ENEMY_MAXHP, 18, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("빨강")
ENDSELECT

PRINTFORM  ({ENEMY_HP, 5}/{ENEMY_MAXHP, 5}) 

PRINTFORML 
DRAWLINE
PRINTFORML %CALLNAME:MASTER%의 턴!
CALL ASK_M("약공격", 1,"강공격", 1,"시간정지 공격", BASE:MASTER:TSP >= 500)
전회행동 = RESULT
SELECTCASE RESULT
	CASE 0
		CALL BATTLE_ATK(2500, 기초공격력 * 100)
	CASE 1
		CALL BATTLE_ATK(1500, 기초공격력 * 200)
		웨이트 = 500
	CASE 2
		PRINTFORML %CALLNAME:MASTER%의 시간정지 공격!
		ENEMY_HP -= 기초공격력 * 200
		PRINTFORMW %ENEMY_NAME%에게 {기초공격력 * 200}의 지대미!
ENDSELECT

SIF ENEMY_HP <= 0
	RETURN 1
CALL BATTLE_DEF(웨이트)
IF BASE:MASTER:체력 <= 500
	RETURN -1
ELSE
	GOTO TURN_START
ENDIF

;--------------------------------------------
@BATTLE_ATK(제한시간, DAMAGE)
;--------------------------------------------
#DIM 제한시간
#DIM 정답
#DIM DAMAGE
#DIM ANSWER, 2
정답 = RAND:4
LOCALS:0 = 상
LOCALS:1 = 오른쪽
LOCALS:2 = 왼쪽
LOCALS:3 = 하
SETCOLOR C_YELLOW
PRINTFORM %LOCALS:정답%
RESETCOLOR
PRINTFORML 를 노려라!
GETMILLISECOND
ANSWER = RESULT
제한시간 += ENEMY_SKILL_WAIT
CALL ASK_BATTLE(제한시간)

IF RESULT == 정답
	GETMILLISECOND
	ANSWER:1 = 제한시간 - (RESULT - ANSWER)
	PRINTFORMW 남은 시간{ANSWER:1}
	IF ANSWER:1 >= 800
		CALL COLORMESSAGE("CRITICAL HIT!", C_AQUA, 2,1)
		DAMAGE = DAMAGE * 3 / 2
	ENDIF
	ENEMY_HP -= DAMAGE
	PRINTFORMW %ENEMY_NAME%에게 {DAMAGE}의 지대미!
ELSEIF RESULT == -1
	PRINTFORMW 미스!
ELSE
	PRINTFORML 가이드 되었다!
	ENEMY_HP -= DAMAGE / 5
	PRINTFORMW %ENEMY_NAME%에게 {DAMAGE / 5}의 지대미!
ENDIF

;--------------------------------------------
@BATTLE_DEF(웨이트)
;--------------------------------------------

#DIM 전회행동
#DIM 웨이트
#DIM 정답
#DIM 제한시간
#DIM DAMAGE
#DIM ANSWER, 2
CALL ENEMY_SKILL_DECIDE
DAMAGE = ENEMY_ATK * ENEMY_SKILL_ATK
정답 = ENEMY_ATK_DIRECTION
LOCALS:0 = 상
LOCALS:1 = 오른쪽
LOCALS:2 = 왼쪽
LOCALS:3 = 하

PRINTFORM %ENEMY_NAME%의 
SETCOLOR C_YELLOW
PRINTFORM %LOCALS:정답%
RESETCOLOR
PRINTFORML 로부터의 %ENEMY_SKILL_NAME%!
GETMILLISECOND
ANSWER = RESULT
제한시간 = ENEMY_SKILL_SPD - 웨이트
CALL ASK_BATTLE(제한시간)

IF RESULT == 정답
	GETMILLISECOND
	ANSWER:1 = 제한시간 - (RESULT - ANSWER)
	PRINTFORMW 남은 시간{ANSWER:1}
	IF ANSWER:1 >= 800
		CALL COLORMESSAGE("JUST GUARD!", C_AQUA, 2,1)
		DAMAGE /= 4
	ELSE
		CALL COLORMESSAGE("GUARD!", C_YELLOW, 2,1)
		DAMAGE /= 2
	ENDIF
ELSEIF RESULT == 3 - 정답
	GETMILLISECOND
	ANSWER:1 = 제한시간 - (RESULT - ANSWER)
	PRINTFORMW 남은 시간{ANSWER:1}
	IF ANSWER:1 >= 800
		CALL COLORMESSAGE("AVOID!", C_AQUA, 2,1)
		DAMAGE = 0
	ENDIF
ELSEIF RESULT == -1
	PRINTFORMW CRITICAL HIT!
	DAMAGE = DAMAGE * 3 / 2
ENDIF
BASE:MASTER:체력 -= DAMAGE
PRINTFORMW %CALLNAME:MASTER%에게 {DAMAGE}의 지대미!　(나머지{BASE:MASTER:체력})
;--------------------------------------------
@SET_ENEMY
;--------------------------------------------
SELECTCASE ENEMY_NUM
CASE 1
	ENEMY_NAME = 샌드 가방
	ENEMY_ATK = 1
	ENEMY_HP = 1000
CASE 2
	ENEMY_NAME = 송사리 적
	ENEMY_ATK = 2
	ENEMY_HP = 2000
ENDSELECT
ENEMY_MAXHP = ENEMY_HP
;--------------------------------------------
@ENEMY_SKILL_DECIDE
;--------------------------------------------
SELECTCASE ENEMY_NUM
CASE 1
	LOCAL = 1
CASE 2
	IF !RAND:4
		LOCAL = 3
	ELSEIF !RAND:2
		LOCAL = 4
	ELSE
		LOCAL = 2
	ENDIF
ENDSELECT
CALL ENEMY_SKILL(LOCAL)
;--------------------------------------------
@ENEMY_SKILL(ARG)
;--------------------------------------------
ENEMY_SKILL_WAIT = 0
SELECTCASE ARG
CASE 1
	ENEMY_SKILL_NAME = 초라한 공격
	ENEMY_SKILL_ATK = 50
	ENEMY_SKILL_SPD = 4000
	ENEMY_ATK_DIRECTION = RAND:2 + 1
CASE 2
	ENEMY_SKILL_NAME = 통상 공격
	ENEMY_SKILL_ATK = 150
	ENEMY_SKILL_SPD = 2000
	ENEMY_ATK_DIRECTION = RAND:4
CASE 3
	ENEMY_SKILL_WAIT = 500
	ENEMY_SKILL_NAME = 강타
	ENEMY_SKILL_ATK = 300
	ENEMY_SKILL_SPD = 2000
	ENEMY_ATK_DIRECTION = 1
CASE 4
	ENEMY_SKILL_WAIT = -500
	ENEMY_SKILL_NAME = 잽
	ENEMY_SKILL_ATK = 100
	ENEMY_SKILL_SPD = 1500
	ENEMY_ATK_DIRECTION = 2
ENDSELECT
;--------------------------------------------
@ASK_BATTLE(제한시간)
;--------------------------------------------
#DIM 제한시간
[SKIPSTART]
PRINTFORML 
PRINTL       [0]↑        
PRINTL   [2]←     [1]→
PRINTL       [3]↓
PRINTFORML 
[SKIPEND]
PRINTFORML            w
PRINT          
PRINTBUTTON "[↑] ", "w"
PRINTFORML 
PRINT  　a 
PRINTBUTTON "[←] ", "a"
PRINTFORM    
PRINTBUTTON "[→] ", "d"
PRINTFORML d
PRINT          
PRINTBUTTON "[↓] ", "s"
PRINTFORML 
PRINTFORML            s
TONEINPUTS 제한시간, "p", 1
SELECTCASE RESULTS
	CASE "w"
		RETURN 0
	CASE "a"
		RETURN 2
	CASE "d"
		RETURN 1
	CASE "s"
		RETURN 3
	CASEELSE
		RETURN -1
ENDSELECT
