;오늘의팬티에는 평상복용과 파자마용의 2종류가 있으므로, 직접 P_ID를 건네주도록(듯이) 개변
@오늘의브래지어(C_ID, P_ID)
#FUNCTION
#LOCALSIZE 2
#LOCALSSIZE 1
#DIM RAND_BRA, MAXBRA
#DIM B_TABLE

#DIM C_ID	;대상 캐릭터
#DIM P_ID	;코디네이트 된 팬티
#DIM B_ID	;브라의 종류

B_ID = GET_INT(C_ID, "하반신속옷_비켜놓기가능", P_ID, "관련 속옷")

IF B_ID == 0 && !(P_ID == 0 || P_ID == 18)
	VARSET RAND_BRA , 0
	FOR LOCAL:0 , 1 , MAXBRA
		RAND_BRA:(LOCAL:0) = LOCAL:0
	NEXT
	FOR LOCAL:0 , 1 , MAXBRA
		LOCAL:1 = RAND(LOCAL:0 , MAXBRA)
		SWAP RAND_BRA:(LOCAL:0), RAND_BRA:(LOCAL:1)
	NEXT
	
	FOR B_TABLE, 0, MAXBRA
		IF GET_INT(C_ID, "상반신속옷_탈의불가", RAND_BRA:B_TABLE, "붙인다")
			B_ID = RAND_BRA:B_TABLE
			BREAK
		ELSE
			CONTINUE
		ENDIF
		SIF B_TABLE == MAXBRA - 1
			THROW %CALLNAME:C_ID%의 브라 추첨에 실패
	NEXT
ENDIF

SIF DECIDE_NOBRA(C_ID)
	B_ID = 0
RETURNF B_ID

;PRINTFORML %CALLNAME:C_ID%:하반신속옷2:{P_ID}:%PANTSNAME(P_ID)%　상반신속옷1:{EQUIP:C_ID:상반신속옷1}:%CLOTHNAME(7, EQUIP:C_ID:7)%

@DECIDE_NOBRA(ARG)
#FUNCTION
#DIM DYNAMIC NOBRA_RATE
SIF (TALENT:ARG:걸레 || TALENT:ARG:소악마 || TALENT:ARG:섹프 || ABL:ARG:노출벽 >= 4) && !RAND:5
	RETURNF 1
SIF TALENT:ARG:모유체질 && !(TALENT:ARG:섹프 || TALENT:ARG:애욕 || ABL:ARG:노출벽 >= 4)
	RETURNF 0
;DAY:2 계절(1=춘 2=하 3=추 4=동)
SELECTCASE DAY:2
CASE 1
	NOBRA_RATE = 8
CASE 2
	NOBRA_RATE = 15
CASE 3
	NOBRA_RATE = 3
CASE 4
	NOBRA_RATE = 1
ENDSELECT

SIF TALENT:ARG:유치 || TALENT:ARG:애같음
	TIMES NOBRA_RATE, 1.20
SIF TALENT:ARG:도착적
	TIMES NOBRA_RATE, 1.20
SIF TALENT:ARG:소악마
	TIMES NOBRA_RATE, 1.20
SIF TALENT:ARG:마조
	TIMES NOBRA_RATE, 1.80
SIF TALENT:ARG:걸레
	TIMES NOBRA_RATE, 2.00

{
	NOBRA_RATE = NOBRA_RATE * 
				(10 + TALENT:ARG:성적흥미 + TALENT:ARG:자기애 + TALENT:ARG:쾌감응답 
					- TALENT:ARG:B감도 - TALENT:ARG:정조 - TALENT:ARG:수치심 - TALENT:ARG:가슴사이즈 ) / 10
}

SIF TALENT:ARG:키스미경험
	TIMES NOBRA_RATE, 0.30
SIF GET_TALENTNAME(GETNUM(TALENT, "담력"), TALENT:ARG:담력) == "겁장이"
	TIMES NOBRA_RATE, 0.50
SIF GET_TALENTNAME(GETNUM(TALENT, "자존심"), TALENT:ARG:자존심) == "자존심 높음"
	TIMES NOBRA_RATE, 0.50

RETURNF RAND:100 <= NOBRA_RATE

@BRANAME(ARG, CHARA)
#FUNCTIONS
#DIM CHARA

CALLF 상반신속옷체크(ARG)
RETURNF GET_STR(CHARA, CLOTHES_PARTS_TO_CATEGORY("상반신속옷1"), ARG, "이름")
