;-------------------------------------------------
;낚시를 한다
;외출 커맨드
;-------------------------------------------------
@COM621
#DIM 잡힌것
#DIM 낚시파워
#DIM 낚시파워M
#DIM 낚시파워T
#DIM 잡히지않는보정
#DIM 낚시운
#DIM CHARA
#DIM 상대자가낚시했다
#DIM 活性
#DIM FISHED, 2

상대자가낚시했다 = 0
;참가자 판정
IF FLAG:데이트상대
	CHARA = FLAG:데이트상대
ELSEIF TARGET && ABL:TARGET:친밀 >= 5 - TALENT:TARGET:낚시Lv
	CHARA = TARGET
ELSE
	CHARA = 0
ENDIF
;불쌍해；；
IF CHARA == [[와카사기히메]] || TARGET == [[와카사기히메]]
	PRINTFORMW 그녀 앞에서 낚시를 하는 것은 가혹한 것이다…
	RETURN -1
ENDIF

;季節ごとの活性
SELECTCASE DAY:2
	CASE 1  ;春
		活性 = 10
	CASE 2  ;夏
		活性 = 7
	CASE 3  ;秋
		活性 = 10
	CASE 4  ;冬
		活性 = 3
ENDSELECT
;時刻ごとの活性補正
SELECTCASE TIME:2
	CASE 1, 4  ;早朝と昼下がり
		活性 += 10
	CASE 5, 6  ;夕と夜
		活性 += 5
ENDSELECT
;天気による活性
SELECTCASE TIME:5
	CASE 3, 6 ;曇、霧雨
		活性 += 5
	CASE 4, 5 ;雨
		活性 += 7
	CASE 8 TO 13 ;雪系統はマイナス
		活性 -= 3
ENDSELECT
;虹が出てるときは、雨の後で水が濁っているだろう
SELECTCASE TIME:7
	CASE 1
		活性 += 7
ENDSELECT

;일일로―50~+50, 아이템이나 기원등으로 보정을 걸치는 일도 가능
낚시운 = FLAG:일일이벤트 / 10 - 50

;釣り技能で活性を察知
IF TALENT:MASTER:낚시Lv >= 3
	SETCOLOR C_L_GRAY
	SELECTCASE (活性 + 낚시운)
		CASE IS <= 10
			PRINTFORML （魚影がほとんど見えない…）
		CASE 11 TO 30
			PRINTFORML （魚影はまばらだ…）
		CASE 31 TO 50
			PRINTFORML （魚影はそれなりに見える…）
		CASE 51 TO 65
			PRINTFORML （魚影はかなり多く見える…）
		CASEELSE
			PRINTFORML （魚影がひしめき、水面がざわついている…）
	ENDSELECT
	RESETCOLOR
ENDIF

;낚시의 묘미 (참다운 즐거움)은 대기 시간이라고 들었다
FOR LOCAL, 0, 5
	REUSELASTLINE  \@ CHARA ?  %조사처리(CALLNAME:CHARA,"과")% 함께 #  \@낚시중. %". " * LOCAL%
	TWAIT 150, 1
NEXT
;낚시파워설정
낚시파워M = 10 + TALENT:MASTER:낚시Lv * 15 + 낚시운 + 잡히지않는보정
SIF CHARA
	낚시파워T = 10 + TALENT:CHARA:낚시Lv * 15 + 잡히지않는보정

;(낚시파워)%로 잡힌다
FISHED:0 = RAND:100
IF FISHED < 낚시파워M
	낚시파워= 낚시파워M
	$FISH
	;잡히면(자) 보정 리셋
	잡히지않는보정 = 0
	;낚시파워로 잡히는 것이 바뀐다
	SELECTCASE MIN(RAND:낚시파워, 100)
	CASE IS < 5
		;목재
		잡힌것 = 610
	CASE IS < 10
		;모리야부적
		잡힌것 = 131
	CASE 51 TO 60
		;개구리
		잡힌것 = 706
	CASE 10 TO 50, 61 TO 89
		FISHED:1 = (FISHED:0 + 10) / 10 * 10
		잡힌것 = FISHING_CATCH(FISHED:1)
	CASE IS < 95
		잡힌것 = FISHING_CATCH(95)
	CASEELSE
		잡힌것 = FISHING_CATCH(100)
		;보너스
		IF 상대자가낚시했다
			EXP:CHARA:낚시경험 ++
		ELSE
			EXP:MASTER:낚시경험 ++
		ENDIF
	ENDSELECT
	;아이템에 추가
	SELECTCASE 잡힌것
		CASE 1 TO 10, 22 TO 23
			CALL GET_ITEM("물고기",1,1)
		CASE 11 TO 20, 24 TO 60
			CALL GET_ITEM("맛있는물고기",1,1)
		CASE 21
			CALL GET_ITEM("로터",1,1)
		CASEELSE
			SIF ITEM:잡힌것 < 99
			ITEM:잡힌것 ++
	ENDSELECT
;CHARA가 낚시했을 경우
ELSEIF CHARA && RAND:100 < 낚시파워T
	낚시파워= 낚시파워T
	상대자가낚시했다 = 1
	GOTO FISH
;잡히지 않았으면 보정 가산, 잡힐 때까지 축적해 나간다
ELSE
	잡히지않는보정 += 10
	잡힌것 = 0
ENDIF

;기본의 경험
EXP:MASTER:낚시경험 ++

SIF ITEM:107 && !RAND:2
	EXP:MASTER:낚시경험 ++

SIF CHARA
	EXP:CHARA:낚시경험 ++
;낚시한 사람의 추가 경험
IF 잡힌것
	IF 상대자가낚시했다
		EXP:CHARA:낚시경험 ++
	ELSE
		EXP:MASTER:낚시경험 ++
	ENDIF
ENDIF
;데이트경험
IF CHK_DATENOW(CFLAG:CHARA:데이트중) && CHARA > 0
	CALL 붉은실발동
	EXP:MASTER:데이트경험 ++
	EXP:CHARA:데이트경험 ++
ENDIF

;당신이 낚시하면(자) 체력 감소
SIF 잡힌것 && !상대자가낚시했다
	DOWNBASE:MASTER:체력 = 50
DOWNBASE:MASTER:기력 = 100

;참가자의 소스, 적당하게 설정하고 있습니다, 동일 커맨드 제한을 받지 않기 때문에 조심스럽게, 다만 잡히지 않으면 매회 반감
IF CHARA > 0
	;고정으로 획득하는 소스
	SOURCE:CHARA:환락 = 400
	SOURCE:CHARA:사랑 = 50
	
	;ABL:순종을 본다
	IF ABL:순종 <= 1
		SOURCE:CHARA:환락 += (ABL:순종 * 40)
		SOURCE:CHARA:사랑 += (ABL:친밀 * 20)
	ELSEIF ABL:순종 <= 3
		SOURCE:CHARA:환락 += 200 + (ABL:순종 * 40)
		SOURCE:CHARA:사랑 += 50 + (ABL:친밀 * 30)
	ELSEIF ABL:순종 <= 5
		SOURCE:CHARA:환락 += 500 + (ABL:순종 * 40)
		SOURCE:CHARA:사랑 += 200 + (ABL:친밀 * 30)
	ELSEIF ABL:순종 <= 8
		SOURCE:CHARA:환락 += 750 + (ABL:순종 * 60)
		SOURCE:CHARA:사랑 += 300 + (ABL:친밀 * 40)
	ELSEIF ABL:순종 <= 11
		SOURCE:CHARA:환락 += 1000 + (ABL:순종 * 60)
		SOURCE:CHARA:사랑 += 600 + (ABL:친밀 * 40)
	ELSE
		SOURCE:CHARA:환락 += 1600 + (ABL:순종 * 30)
		SOURCE:CHARA:사랑 += 1000 + (ABL:친밀 * 20)
	ENDIF
	SIF CHARA != FLAG:데이트상대 
		SOURCE:CHARA:사랑 = 0
	
	;호감 도는 보지 않는다
	
	
	SOURCE:CHARA:수동 = 200 + 100 * ABL:순종
	SOURCE:CHARA:정복 = 200 + 100 * ABL:새드끼
	
	;낚시질의 성과를 본다
	SELECTCASE 잡힌것
	;잡히지 않는다
	CASE 0
		TIMES SOURCE:CHARA:환락 , 0.50
		TIMES SOURCE:CHARA:수동 , 0.50
		TIMES SOURCE:CHARA:정복 , 0.50
	;목재, 모리야부적
	CASE 610, 131
		TIMES SOURCE:CHARA:환락 , 0.10
		TIMES SOURCE:CHARA:수동 , 0.50
		TIMES SOURCE:CHARA:정복 , 0.50
		SOURCE:CHARA:달성 = 20
	;황어, 아마고, 송어 , 이와나
	CASE 1 TO 10, 22
		TIMES SOURCE:CHARA:환락 , 1.00
		TIMES SOURCE:CHARA:수동 , 1.00
		TIMES SOURCE:CHARA:정복 , 1.00
		SOURCE:CHARA:달성 = 100
	;개구리
	CASE 706
		IF CHARA == [[치르노]]
			TIMES SOURCE:CHARA:환락 , 2.00
			TIMES SOURCE:CHARA:수동 , 2.00
			TIMES SOURCE:CHARA:정복 , 2.00
		ELSEIF CHARA == [[스와코]]
			TIMES SOURCE:CHARA:환락 , 0.50
			TIMES SOURCE:CHARA:수동 , 0.50
			TIMES SOURCE:CHARA:정복 , 0.50
		ELSE
			TIMES SOURCE:CHARA:환락 , 1.25
			TIMES SOURCE:CHARA:수동 , 1.00
			TIMES SOURCE:CHARA:정복 , 1.00
		ENDIF
		SOURCE:CHARA:달성 = 150
	;술, 빙어, 니지마스, 캄르치
	CASE 11 TO 13, 15 TO 20, 24 TO 30
		TIMES SOURCE:CHARA:환락 , 1.50
		TIMES SOURCE:CHARA:수동 , 1.50
		TIMES SOURCE:CHARA:정복 , 1.50
		SOURCE:CHARA:달성 = 200
	;칠성장어
	CASE 14
		;みすちー
		IF CHARA == 28
			TIMES SOURCE:CHARA:환락 , 2.00
			TIMES SOURCE:CHARA:수동 , 2.00
			TIMES SOURCE:CHARA:정복 , 2.00
		ELSE
			TIMES SOURCE:CHARA:환락 , 1.50
			TIMES SOURCE:CHARA:수동 , 1.50
			TIMES SOURCE:CHARA:정복 , 1.50
		ENDIF
		SOURCE:CHARA:달성 = 250
	;로터
	CASE 21
		SOURCE:CHARA:욕정 = 200
	;실러캔스，왕연어，얼음의비늘, 용의손톱，龍涎香
	CASE 23, 31, 609, 606, 643
		TIMES SOURCE:CHARA:환락 , 2.00
		TIMES SOURCE:CHARA:수동 , 2.00
		TIMES SOURCE:CHARA:정복 , 2.00
		SOURCE:CHARA:달성 = 300
	ENDSELECT
ENDIF

;시간경과
TIME += 20

;플래그 인도
TFLAG:SELECTCOM분기 = 잡힌것
TFLAG:SOURCE등에영향을주지않는SELECTCOM분기 = CHARA + (상대자가낚시했다 * 1000)

RETURN 1

;-------------------------------------------------
;실행 가능 판정
;-------------------------------------------------
;낚시를 한다
@COM_ABLE621
;일괄관리
SIF GLOBAL_COMABLE(621)
	RETURN RESULT
;낚싯대가 없다
SIF !ITEM:낚싯대
	RETURN 0
;거점에서는 할 수 없다(가짜)
SIF !FISHING_SPOT(CFLAG:MASTER:현재위치)
	RETURN 0
;데이트중은 데이트상대와 만
SIF FLAG:데이트상대 && TARGET != FLAG:데이트상대
	RETURN 0
;동침중
SIF CFLAG:MASTER:동침중
	RETURN 0
;우후후中
SIF CFLAG:MASTER:우후후
	RETURN 0
;수면중
SIF CFLAG:MASTER:수면
	RETURN 0
;시간정지중
SIF FLAG:70
	RETURN 0
RETURN 1

;FileName_DATE_CMN.ERB ------------------------------- Rev1. 00
;낚시가 생기는 장소
;CALL		USER
;ARG		현재위치
;RETURN		할 수 있다면 1
;COMMENT	장소를 추가했을 경우, CASE를 변경의 일 현재 KR판에만 있음
;-----------------------------------------------------------
@FISHING_SPOT(ARG)
#FUNCTION
IF !IN_HOME(ARG)
	SELECTCASE ARG
		CASE 안개의호수
			RETURNF 1
		CASE 삼도천
			RETURNF 2
		CASE 현무의늪
			RETURNF 3
		CASE 큰두꺼비의연못
			RETURNF 4
		CASE 구천의폭포
			RETURNF 5
		CASE 산의호수
			RETURNF 6
		CASE 지옥의깊은길
			RETURNF 7
	ENDSELECT
ELSE
	SELECTCASE ARG
		CASE 347;霧の湖
			RETURNF 1
		CASE 602;賽の河原
			RETURNF 2
		CASE 703;玄武の沢
			RETURNF 3
		CASE 708;大蝦蟇の池
			RETURNF 4
		CASE 806;滝の裏
			RETURNF 5
		CASE 814;湖畔（山の湖）
			RETURNF 6
		CASE 945;川岸
			RETURNF 7
		CASE 1068;純狐邸中庭
			RETURNF 8
	ENDSELECT
ENDIF
RETURNF 0

;-------------------------------------------------
;釣れたもの判定の拡張　場所、季節、時刻に応じて可変
;ARG　…ランクに対応
;-------------------------------------------------
@FISHING_CATCH(ARG)
#FUNCTION

SELECTCASE ARG
	CASE 20
		SELECTCASE FISHING_SPOT(CFLAG:MASTER:현재위치)
			CASE 1, 4 ;버들치
				RETURNF 5
			CASE 2, 7, 8 ;피라미
				RETURNF 6
			CASE 3, 5, 6 ;황어
				RETURNF 1
		ENDSELECT
	CASE 30
		SELECTCASE FISHING_SPOT(CFLAG:MASTER:현재위치)
			CASE 1, 2, 6 ;까나리
				RETURNF 7
			CASE 3, 5 ;송어
				RETURNF 2
			CASE 4 ;개구리
				RETURNF 706
			CASE 7 ;피라니아
				RETURNF 8
		ENDSELECT
	CASE 40
		SELECTCASE FISHING_SPOT(CFLAG:MASTER:현재위치)
			CASE 1, 2, 6 ;둑중개
				RETURNF 16
			CASE 3, 5 ;산천어ちゃん
				RETURNF 3
			CASE 4, 8 ;개구리
				RETURNF 706
			CASE 7 ;가아
				RETURNF 9
		ENDSELECT
	CASE 50
		SELECTCASE FISHING_SPOT(CFLAG:MASTER:현재위치)
			CASE 1, 6 ;블루길
				RETURNF 10
			CASE 2, 7, 8 ;은어（春夏）or피라미（秋冬）
				IF DAY:2 < 3
					RETURNF 17
				ELSE
					RETURNF 6
				ENDIF
			CASE 3, 5 ;곤들매기
				RETURNF 4
			CASE 4 ;개구리
				RETURNF 706
		ENDSELECT
	CASE 70
		SELECTCASE FISHING_SPOT(CFLAG:MASTER:현재위치)
			CASE 1, 4, 6 ;메기or빙어（冬）
				IF DAY:2 == 4
					RETURNF 12
				ELSE
					RETURNF 18
				ENDIF
			CASE 2, 7, 8 ;뱀장어
				RETURNF 19
			CASE 3, 5 ;무지개송어or연어（秋）or빙어（冬）
				IF DAY:2 == 3
					RETURNF 11
				ELSEIF DAY:2 == 4
					RETURNF 12
				ELSE
					RETURNF 13
				ENDIF
		ENDSELECT
	CASE 80
		SELECTCASE FISHING_SPOT(CFLAG:MASTER:현재위치)
			CASE 1 ;각시송어
				RETURNF 20
			CASE 2, 4, 6, 8 ;잉어
				RETURNF 22
			CASE 3, 5 ;칠성장어
				RETURNF 14
			CASE 7 ;철갑상어
				RETURNF 24
		ENDSELECT
	CASE 90
		SELECTCASE FISHING_SPOT(CFLAG:MASTER:현재위치)
			CASE 1 ;쿠니마스
				RETURNF 25
			CASE 2 ;초어
				RETURNF 26
			CASE 3 ;가물치
				RETURNF 15
			CASE 4 ;떡붕어
				RETURNF 27
			CASE 5 ;일본연어
				RETURNF 28
			CASE 6 ;큰입배스
				RETURNF 29
			CASE 7 ;피라루쿠
				RETURNF 30
			CASE 8 ;강청어
				RETURNF 32
		ENDSELECT
	CASE 95
		SELECTCASE  FISHING_SPOT(CFLAG:MASTER:현재위치)
			CASE 1 ;왕연어（新月の日）or로터
				IF MOON_WATCH(DAY:3) == 1
					RETURNF 31
				ELSE
					RETURNF 21
				ENDIF
			CASE 2 ;실러캔스
				RETURNF 23
			CASEELSE
				;로터
				RETURNF 21
		ENDSELECT
	CASE 100
		SELECTCASE FISHING_SPOT(CFLAG:MASTER:현재위치)
			CASE 1, 6, 8 ;龍涎香or얼음의비늘（冬）
				IF DAY:2 == 4
					RETURNF 609
				ELSE
					RETURNF 643
				ENDIF
			CASE 2, 3, 5, 7 ;용의손톱or얼음의비늘（冬）
				IF DAY:2 == 4
					RETURNF 609
				ELSE
					RETURNF 606
				ENDIF
			CASE 4 ;개구리
				RETURNF 706
		ENDSELECT
ENDSELECT
RETURNF 0

;-----------------------------------------------------------
;釣った魚のナンバーを名称に変換
;コレクションリストにも利用するため、로터は除外しておく
;ARG　…釣れたものナンバーに対応
;-----------------------------------------------------------
@FISH_NAME(ARG)
#FUNCTIONS

SELECTCASE ARG
	CASE 1
		RETURNF "황어"
	CASE 2
		RETURNF "송어"
	CASE 3
		RETURNF "산천어"
	CASE 4
		RETURNF "곤들매기"
	CASE 5
		RETURNF "버들치"
	CASE 6
		RETURNF "피라미"
	CASE 7
		RETURNF "까나리"
	CASE 8
		RETURNF "피라니아"
	CASE 9
		RETURNF "가아"
	CASE 10
		RETURNF "블루길"
	CASE 11
		RETURNF "연어"
	CASE 12
		RETURNF "빙어"
	CASE 13
		RETURNF "무지개송어"
	CASE 14
		RETURNF "칠성장어"
	CASE 15
		RETURNF "가물치"
	CASE 16
		RETURNF "둑중개"
	CASE 17
		RETURNF "은어"
	CASE 18
		RETURNF "메기"
	CASE 19
		RETURNF "뱀장어"
	CASE 20
		RETURNF "각시송어"
	CASE 22
		RETURNF "잉어"
	CASE 23
		RETURNF "실러캔스"
	CASE 24
		RETURNF "철갑상어"
	CASE 25
		RETURNF "쿠니마스"
	CASE 26
		RETURNF "초어"
	CASE 27
		RETURNF "떡붕어"
	CASE 28
		RETURNF "일본연어"
	CASE 29
		RETURNF "큰입배스"
	CASE 30
		RETURNF "피라루쿠"
	CASE 31
		RETURNF "왕연어"
	CASE 32
		RETURNF "강청어"
	CASEELSE
		RETURNF ""
ENDSELECT

;-------------------------------------------------
;釣れた魚のサイズを一般的サイズ内でランダムに出力する変数
;ARG　…釣れたものナンバーに対応
;-------------------------------------------------
@FISHING_SIZE(ARG)
#FUNCTION

LOCAL = 0
SELECTCASE ARG
	CASE 1 ;황어
		LOCAL = MAX(RAND:(56), 25)
	CASE 2 ;송어
		LOCAL = MAX(RAND:(40), 18)
	CASE 3 ;산천어ちゃん
		LOCAL = MAX(RAND:(50), 25)
	CASE 4 ;곤들매기
		LOCAL = MAX(RAND:(70), 20)
	CASE 5 ;버들치
		LOCAL = MAX(RAND:(18), 10)
	CASE 6 ;피라미
		LOCAL = MAX(RAND:(18), 10)
	CASE 7 ;까나리
		LOCAL = MAX(RAND:(36), 15)
	CASE 8 ;피라니아
		LOCAL = MAX(RAND:(61), 15)
	CASE 9 ;가아
		LOCAL = MAX(RAND:(300), 120)
	CASE 10 ;블루길
		LOCAL = MAX(RAND:(28), 12)
	CASE 11 ;연어
		LOCAL = MAX(RAND:(100), 65)
	CASE 12 ;빙어
		LOCAL = MAX(RAND:(18), 8)
	CASE 13 ;무지개송어
		LOCAL = MAX(RAND:(105), 35)
	CASE 14 ;칠성장어
		LOCAL = MAX(RAND:(65), 45)
	CASE 15 ;가물치
		LOCAL = MAX(RAND:(115), 80)
	CASE 16 ;둑중개
		LOCAL = MAX(RAND:(18), 10)
	CASE 17 ;은어
		LOCAL = MAX(RAND:(35), 25)
	CASE 18 ;메기
		LOCAL = MAX(RAND:(80), 50)
	CASE 19 ;뱀장어
		LOCAL = MAX(RAND:(120), 85)
	CASE 20 ;각시송어
		LOCAL = MAX(RAND:(55), 20)
	CASE 21 ;로터
		LOCAL = 5
	CASE 22 ;잉어
		LOCAL = MAX(RAND:(115), 55)
	CASE 23 ;실러캔스
		LOCAL = MAX(RAND:(200), 100)
	CASE 24 ;철갑상어
		LOCAL = MAX(RAND:(301), 100)
	CASE 25 ;쿠니마스
		LOCAL = MAX(RAND:(45), 30)
	CASE 26 ;초어
		LOCAL = MAX(RAND:(140), 70)
	CASE 27 ;떡붕어
		LOCAL = MAX(RAND:(62), 25)
	CASE 28 ;일본연어
		LOCAL = MAX(RAND:(220), 90)
	CASE 29 ;큰입배스
		LOCAL = MAX(RAND:(55), 25)
	CASE 30 ;피라루쿠
		LOCAL = MAX(RAND:(400), 250)
	CASE 31; 왕연어
		LOCAL = MAX(RAND:(550), 300)
	CASE 32; 강청어
		LOCAL =  MAX(RAND:(220), 120)
ENDSELECT
RETURNF LOCAL

;-------------------------------------------------
;釣果の説明、消去行数の関係で５行に統一
;ARG　…釣れたものナンバーに対応
;-------------------------------------------------
@FISHING_EXPLANATION(ARG)
IF INRANGE(ARG, 11, 20) || INRANGE(ARG, 24, 60)
	CALL COLOR_PRINT("　　☆맛있는물고기☆", "C_YELLOW")
ELSE
	PRINTFORM 　　◇普通の魚◇
ENDIF
PRINTL 
SELECTCASE ARG
	CASE 1 ;황어
		PRINTFORML 　　体長30cm～50cm前後、雑食性。背側は焦茶色、春には黒と赤の婚姻色を表す
		PRINTFORML 　　２～４年で成魚となり、群れて生活する。小骨が多く、臭いあり
		PRINTFORML 　　河川の上流から下流、はたまた山上湖にも生息。水質汚染や酸性水にも強い
		PRINTFORML 
		PRINTFORML 
	CASE 2 ;송어
		PRINTFORML 　　体長20cm～30cm前後、小魚や昆虫食。산천어とよく似た姿だが、側面に散在する朱点で区別できる
		PRINTFORML 　　サツキマスの陸封型（海や湖に降りず、河川で一生を終える）で、年間を通して水温の低い場所を好む
		PRINTFORML 　　漢字は「雨子」「雨魚」などが当てられ、梅雨時～初夏など雨がちな時期によく釣れる
		PRINTFORML 　　また、「甘い（美味しいの意）魚」という意味の異称を持つほど、美味である
		PRINTFORML 
	CASE 3 ;산천어ちゃん
		PRINTFORML 　　体長は～40cm前後、ミミズや昆虫食。背側は緑がかった黄褐色、若いうちは小判状の斑紋模様（パーマーク）を示す
		PRINTFORML 　　サクラマスの陸封型（海や湖に降りず、河川で一生を終える）で、河川上流の冷水域に生息
		PRINTFORML 　　寿命は３～４年。生活スタイルの似る곤들매기よりも下流に生息し「棲み分け」を行う
		PRINTFORML 　　非常に警戒心が強く釣るのは難しいとされ、「渓流の女王」の異名をとる
		PRINTFORML 
	CASE 4 ;곤들매기
		PRINTFORML 　　体長20～60cm前後、貪欲な肉食性で、蛇すら食した事例も。背側は緑褐色や灰色で、地域によって様々
		PRINTFORML 　　河川最上流の冷水域に生息し、生活スタイルの似る산천어よりも上流に暮らし「棲み分け」を行う
		PRINTFORML 　　かつては他の연어の仲間と同様に海に降りる生活をしていたようだが、本種はとりわけ冷水を好む性質ゆえ、
		PRINTFORML 　　氷河期の終焉とともに陸封型の生活に固定され、地方地方の河川ごとに分断された結果、多様な亜種が生まれたようだ
		PRINTFORML 
	CASE 5 ;버들치
		PRINTFORML 　　体長～15cm程度、雑食性。体色は黄褐色で鱗が小さく、ぬめりが強いため「アブラ」の名をとる
		PRINTFORML 　　河川の中流から上流域、山地の湖沼など、低水温を好む。群れて産卵をする習性をもつ
		PRINTFORML 　　多少の苦みがあるが、天ぷらやマリネ、南蛮漬けなどに
		PRINTFORML 
		PRINTFORML 
	CASE 6 ;피라미
		PRINTFORML 　　体長～15cm程度、背側は灰白色、繁殖期のオスには青緑とピンクの鮮やかな婚姻色が表れる
		PRINTFORML 　　河川下流から中流域、湖沼などに生息。水質汚染や河川改修による環境変化に強い
		PRINTFORML 　　草食寄りの雑食性。甘露煮や天ぷら、なれずしなどに
		PRINTFORML 
		PRINTFORML 
	CASE 7 ;까나리
		PRINTFORML 　　キンブナは体長15cm程度、ギンブナは30cm程度まで成長。キンブナは黄褐色の体色をしている
		PRINTFORML 　　雑食性で、河川下流域や湖沼などに生息する잉어の仲間
		PRINTFORML 　　マブナとも言われるが、これはゲンゴロウブナ（떡붕어）と他の까나리を区別するための総称
		PRINTFORML 　　까나리の仲間の分類は非常に難しく、いまだに定説はないとされている
		PRINTFORML 
	CASE 8 ;피라니아
		PRINTFORML 　　アマゾン川などの熱帯地方に生息する、鋭い歯を持つ肉食魚の総称
		PRINTFORML 　　現地の言葉で「歯のある魚」の意味で、小型種では15cmほど、大型種では60cmほどまで成長する
		PRINTFORML 　　「人食い魚」は誇張されたイメージで、実態は臆病。群れて暮らすが、群れ全体が興奮状態に入ると集団で攻撃を開始する
		PRINTFORML 　　肉はタンパク質豊富で美味。地底の地熱で温かい川に適応しているのだろうか？
		PRINTFORML 
	CASE 9 ;가아
		PRINTFORML 　　堅牢な硬鱗（ガノイン鱗）を持つ、魚類の中でも原始的なスタイルの魚。北アメリカ大陸の原産
		PRINTFORML 　　細長い胴体と細長く伸びるアゴを持ち、種によっては３ｍに迫る巨体にまで成長することがある
		PRINTFORML 　　近年日本の某河川でも排水（温水）によって定着しているのではないかと言われている
		PRINTFORML 　　地底の地熱で温かい川に適応しているようだが……誰が持ち込んだのだろう？
		PRINTFORML 
	CASE 10 ;블루길
		PRINTFORML 　　北米大陸が原産の魚。体長～25cm程度。湖沼や流れの緩やかな河川に生息する
		PRINTFORML 　　パンフィッシュ（フライパンに収まりやすい魚）と俗称されるグループの魚だけあり、丸っこく扁平な形をしている
		PRINTFORML 　　非常に悪食で、何でも食べる貪欲さが移植先の水棲生物に大きな影響を与えてしまい、駆除の対象となっている
		PRINTFORML 　　幻想郷にも分布を広げているのだとすれば、大変なことになりそうだ
		PRINTFORML 
	CASE 11 ;연어
		PRINTFORML 　　体長70cm程度まで成長。川でふ化した稚魚は3～4月に海に降り、非常に長い距離を回遊しながら成長する
		PRINTFORML 　　４～６年ほど海で過ごした後、秋ごろに生まれた川を遡上し、湧水のある河床に産卵、一生を終える
		PRINTFORML 　　遡上してくる産卵期の個体の食味は卵や白子の形成によって脂・旨味が落ちており、沖合で漁獲されたものに及ばない
		PRINTFORML 　　……海のないはずの幻想郷で、一体何処でこれほどの成熟を遂げてくるのだろうか？
		PRINTFORML 
	CASE 12 ;빙어
		PRINTFORML 　　体長は成魚で15cm程度。湖沼やそれに注ぐ河川に生息。一部の個体は海に降りることもある
		PRINTFORML 　　冬に産卵を行う一年魚で、冬・氷上の釣りがイメージされるが、実際は一年を通して釣れる
		PRINTFORML 　　冷水を好むとともに、存外に塩分や水質汚染にも強く、大量産卵をすることで多くの仲間を増やす繁殖力を併せ持つ
		PRINTFORML 　　食用にも向いており、諏訪湖をはじめ多くの湖に移植された
		PRINTFORML 
	CASE 13 ;무지개송어
		PRINTFORML 　　別名レインボートラウト。体長は一般には40cm程度だが、湖などに住むものは80cm～1ｍ近くまで成長する
		PRINTFORML 　　産卵しても１回では死なず、数年にわたって産卵を行うことができる
		PRINTFORML 　　寄生虫がいないと言われ、刺身でも食べられる珍しい淡水魚で、繁殖期のオスには虹色の婚姻色が発現する
		PRINTFORML 　　日本に移入されたのは明治時代。幻想郷が大結界で隔てられた後の事だが……どこから入ってきたのだろう？
		PRINTFORML 
	CASE 14 ;칠성장어
		PRINTFORML 　　体長は50～60cm。無顎類（アゴを持たない魚類）と呼ばれる極めて原始的な魚類で「生きた化石」
		PRINTFORML 　　体側に7対のエラの穴があり、それが目のように見えることから「八つ目」の名を持つ
		PRINTFORML 　　清浄な冷水を好み、降海型と陸封型が存在する。なお、뱀장어とは完全に別種である
		PRINTFORML 　　食味は硬くクセはあるが栄養は豊富で、ビタミンＡの一種を多量に含み、夜盲症に絶大な効果があるという
		PRINTFORML 
	CASE 15 ;가물치
		PRINTFORML 　　別名ライギョ。体長は１ｍ程度まで成長する。水流が緩やかで、水草などが多い環境を好む
		PRINTFORML 　　魚食性だが、時にはカメや水鳥のヒナをも捕食する悪食。水底に潜み、待ち伏せを行う
		PRINTFORML 　　白身で淡白な味わいで、揚げ物や鍋で食される。タイワンドジョウの別称の通り、ユーラシア大陸が原産の外来種
		PRINTFORML 　　「そちら」の方面からやってきた誰かが持ち込んだものだろうか？
		PRINTFORML 
	CASE 16 ;둑중개
		PRINTFORML 　　カサゴの仲間の淡水魚で体長15cm程度まで成長。体色は褐色～淡褐色まで変化に富む
		PRINTFORML 　　綺麗な水の河川の瀬や砂利底に潜んでいることが多く、水生昆虫や小魚をエサとする
		PRINTFORML 　　見た目は悪いが、とても美味でよいダシが出る。唐揚げや鍋、甘露煮など、広く料理に用いられる
		PRINTFORML 
		PRINTFORML 
	CASE 17 ;은어
		PRINTFORML 　　体長～30cm程度、成魚になると石についた藻類を食べて成長する。１年で寿命を終える魚
		PRINTFORML 　　成魚は縄張りを持ち、侵入した은어を追い払う習性をもつ。これを利用したのが囮の은어を泳がせて釣る「友釣り」
		PRINTFORML 　　本来は川と海を回遊する魚であるが、琵琶湖流域では海の代わりに琵琶湖に降り、遡上して大きく成長するものもいるという
		PRINTFORML 　　海のない幻想郷では、湖での成熟を行っているのだろうか？
		PRINTFORML 
	CASE 18 ;메기
		PRINTFORML 　　体長は60cm程度。河川の中～下流域や湖沼、水田や用水路にも生息。夜行性で、発達した口ヒゲで周囲を探る肉食魚
		PRINTFORML 　　古くから、カエルをつけた仕掛けで水面を叩いて誘う「ポカン釣り」という手法で釣り上げられていた
		PRINTFORML 　　なお、地震の予兆として메기が暴れる、という俗信があるが、과학적な裏付けはまだ取れていないとされる
		PRINTFORML 　　地下に大메기がおり、これが暴れると大地震が起こる、というのも説話として有名……え？　夢に出た？
		PRINTFORML 
	CASE 19 ;뱀장어
		PRINTFORML 　　体長は１ｍ程度まで成長する。河川の中～下流域や湖沼に生息。体表はヌルヌルした粘液に覆われる
		PRINTFORML 　　夜行性の肉食魚で、淡水魚に分類されるが、海に降って産卵・ふ化を行う降河回遊を行う
		PRINTFORML 　　深海でふ化し、海で成長してシラス뱀장어に、その後沿岸から川を遡上して成長を続け、５～１０年で成熟する
		PRINTFORML 　　その姿形から龍の化身、菩薩の遣いになぞらえられることもあり、食すのを禁忌とする地域もあったようだ
		PRINTFORML 　　めっきり外の世界で見なくなったのは、뱀장어が幻想入りしているから、なのだろうか？
	CASE 20 ;각시송어
		PRINTFORML 　　体長は20～50cm。北米大陸に生息するベニザケの陸封型（海や湖に降りず、河川で一生を終える魚）
		PRINTFORML 　　冷水を好むが、水深が深く冷たい湧水がある湖では暖地でも定着するため、いくつかの湖に移植された
		PRINTFORML 　　体色は灰色で、甲殻類などの動物プランクトンを主食とし、빙어とはエサを巡り競合を起こす
		PRINTFORML 　　연어科の魚の中でも最も美味とされるが、すぐに食味が落ちてしまう
		PRINTFORML 
	;CASE 21 ;로터

	CASE 22 ;잉어
		PRINTFORML 　　体長は60cm～１ｍ程度まで成長。規模の大きい河川の下流～中流域、湖沼、用水路などに広く生息
		PRINTFORML 　　低水温や汚染に耐える생명력を持つうえ寿命が長く、70～80年も生き延びる個体も存在する
		PRINTFORML 　　水底を吸い混ぜ、およそ口に入るものなら何でも食べる強い雑食性を示す
		PRINTFORML 　　よく見られる体高の高い잉어は、中国から大昔に移入されたものであるといわれている
		PRINTFORML 　　琵琶湖水系などに生息するスリムな잉어が日本に古来から生息するものと推測され、野鯉と俗称される
	CASE 23 ;실러캔스
		PRINTFORML 　　体長は１ｍを超える。かつて広く世界中に分布した古代魚で、肉質のヒレを数多く持つ
		PRINTFORML 　　現生の陸上脊椎動物は全てこの系統から進化したと言われており、深海から生存種が発見された生きた化石
		PRINTFORML 　　味は申し訳ないがまずい。カニの肉をさらに연한 맛にしたような味、ハブラシのような食味、と評されている
		PRINTFORML 　　この個体は淡水性で、外の世界ではとうに絶滅したはずの種と考えられる……幽霊？
		PRINTFORML 
	CASE 24 ;철갑상어
		PRINTFORML 　　体長１ｍ～４ｍにも成長。形はサメに似るが、サメとは別種の古代魚
		PRINTFORML 　　卵を塩漬けにしたものがキャビアであり、各地で乱獲の憂き目にあった
		PRINTFORML 　　かつて日本にも生息していたが、２０世紀初頭に絶滅したという
		PRINTFORML 　　現代は温泉水の熱などを利用し、外国産の品種の養殖も行われている
		PRINTFORML 
	CASE 25 ;쿠니마스
		PRINTFORML 　　体長30cm～40cm。体色は灰色から黒色。각시송어の亜種とも言われたが別種の可能性もあるという
		PRINTFORML 　　水深の深い湖に生息し、産卵時は30～40ｍの比較的浅い所まで上がってくるという
		PRINTFORML 　　もとは秋田県の田沢湖にのみ生息した固有種であったが、湖に酸性の河川水を引き入れた事により絶滅
		PRINTFORML 　　しかし、昭和初期に山梨県の西湖に卵が移入されており、その子孫が絶滅宣言から70年後に再発見された
		PRINTFORML 　　どう食べても非常に美味、と評されており、かつては特産品として献上されていた
	CASE 26 ;초어
		PRINTFORML 　　体長は1.2ｍ程度、高水温や酸欠にも強い、水草や水際の雑草を食する草食性の잉어の仲間
		PRINTFORML 　　もとは中国の大河に生息しており、上流で産卵した浮遊性の卵が水流に流されるうちにふ化する特性をもつ
		PRINTFORML 　　卵が海まで流されると死んでしまうため、流れの緩やかで長大な流域面積を持つ河でないと繁殖できない
		PRINTFORML 　　中国では四大家魚と称されるほどポピュラーな魚で、蒸し魚・煮魚・揚げ物・スープなど、広く食べられている
		PRINTFORML 
	CASE 27 ;떡붕어
		PRINTFORML 　　琵琶湖水系のゲンゴロウブナの変種を品種改良したもので、横から見るとひし形の形をしている
		PRINTFORML 　　体長は30～60cm程度。寿命は長く、数十年生きるものもいるという
		PRINTFORML 　　「釣りは까나리に始まり까나리に終わる」の金言の、「終わりの까나리」が本種であるとされ、難易度が高い
		PRINTFORML 　　季節により別種であるように行動が変わり、水温や天気によって遊泳層（タナ）も変わるうえ、
		PRINTFORML 　　採餌も溶け出したエサを小さく吸い込むだけで、感度の高いウキや竿を用いなければアタリをとるのも難しい
	CASE 28 ;일본연어
		PRINTFORML 　　体長1～1.5ｍ、時に２ｍを超える日本最大の淡水魚。冷水で流れの緩い河川の中流～下流域に生息
		PRINTFORML 　　背は青みがかった褐色。かつては本州北部にも生息していたが、現在は北海道にのみ分布する
		PRINTFORML 　　日本の일본연어は연어のように降海するが、幻想郷の本種は外国種のように陸封型の生活を送っているようだ
		PRINTFORML 　　肉は白身で脂が乗って美味しいが、成長が遅いのが難点
		PRINTFORML 
	CASE 29 ;큰입배스
		PRINTFORML 　　体長30～50cm、北米原産の肉食魚で、湖沼や河川の中～下流域に生息
		PRINTFORML 　　自分の体長の半分ほどの魚や、小鳥やカエル、ネズミまで丸呑みにしてみせる貪欲さを持つ
		PRINTFORML 　　スポーツフィッシングの対象として有名だが、移入先で生態系を圧迫するなど問題も多い
		PRINTFORML 　　肉は淡白な白身でクセが無く、ムニエルやフライなどでおいしく頂ける
		PRINTFORML 
	CASE 30 ;피라루쿠
		PRINTFORML 　　南米大陸に生息している体長３ｍを超える大魚。魚食性で、丸太のような胴、平たい頭部と尾ひれが特徴
		PRINTFORML 　　世界最大級の淡水魚の一種で、ガーの仲間と並んで古くから姿を変えていない古代魚
		PRINTFORML 　　体色は黒から銀色まで様々で、成魚では後部が赤色に変色する。現地の言葉で「紅い魚」と呼ばれる由縁
		PRINTFORML 　　肉は食べごたえがあり、無数の突起状の骨の通った舌（骨舌）はおろし器、鱗はヤスリや靴べらに、と広く利用された
		PRINTFORML 
	CASE 31; 왕연어
		PRINTFORML 　　体長３ｍ以上にもなるという大怪魚。大型のマスのような姿で口は兎のような三口、下あごが突き出ている
		PRINTFORML 　　冷たく深い湖に生息しているとされ、その実態はいまだ掴めていない謎の魚
		PRINTFORML 　　肉は赤身で多くの脂を含み、簡単には焼けないが非常に美味であるという
		PRINTFORML 　　正体は、全くの新種の魚か、곤들매기の変種か、湖に残留する일본연어か、はたまたキングサーモンか
		PRINTFORML 　　その真相は、今も深く冷たい水の中に隠れている……
	CASE 32;강청어
		PRINTFORML 　　中国四大家魚のひとつ。成長すると体長２ｍにもなるコイの仲間。コイと比べスマートな顔立ちで、ヒゲはない
		PRINTFORML 　　青、と名前にあるが、その体色はどちらかといえば群青～濃紺色をしており、ブラックカープとも呼ばれる
		PRINTFORML 　　水底を吸い混ぜ底生動物を捕食し、特に貝類を好んで食べる雑食性
		PRINTFORML 　　四大家魚の中では最も大きくなり、また最も流通が少ない。蒸し魚、煮魚、揚げ物、などで食される
		PRINTFORML 　　味わいは濃厚だが、小骨があり食べるのに苦労するかも
ENDSELECT
