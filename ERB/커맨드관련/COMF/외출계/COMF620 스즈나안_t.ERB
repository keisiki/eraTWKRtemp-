;대본가게
@COM620
#DIM CHARISMA
#DIM ItemId

$C_LOOP
DRAWLINE
IF !CHARISMA
	PRINTFORML 소지금:%금액표시(MONEY)%
ELSE
	PRINTFORML 소지카리스마:{MONEY:2}
ENDIF
PRINTL 「임차료는 모두 \3000 입니다」
DRAWLINE
FOR LOCAL,1, BookTitles + 1
	ItemId = LOCAL + 100
	PRINTFORM [{LOCAL}] %ITEMNAME:ItemId%
	IF ITEM:ItemId
		PRINTFORML (%마스터가% 빌리고 있다)
	ELSEIF GETBIT (FLAG:1011,LOCAL)
		PRINTL (대출중)
	ELSE
		PRINTL 　
	ENDIF
NEXT

IF !CHARISMA
	PRINTFORML [100] 카리스마 지불
ELSE
	PRINTFORML [100] 현금지불
ENDIF
DRAWLINE
PRINTL [0] 그만둔다

$LOOP
INPUT
FOR LOCAL,1, BookTitles + 1
	ItemId = LOCAL + 100
	IF ITEM:ItemId
		PRINTL 「2권 이상의 대출은 할 수 없습니다」
		RETURN -1
	ENDIF
NEXT

IF RESULT == 100
	IF !CHARISMA
		PRINTFORMW 「카리스마로 지불해???」
		PRINTFORMW 「그러니까……그럼 10 카리스마? 로 괜찮아……」
		IF MONEY:2 < 10
			PRINTFORMW 카리스마가 부족하다
			GOTO C_LOOP
		ENDIF
	ELSE
		PRINTFORMW 「아, 돈으로 지불하는거구나」
	ENDIF
	PRINTFORML 
	CHARISMA = !CHARISMA
	GOTO C_LOOP
ELSE
	IF !CHARISMA && MONEY < 3000
		PRINTW 돈이 모자랐다…
		RETURN -1
	ENDIF
	IF INRANGE(RESULT, 1, BookTitles)
		IF GETBIT (FLAG:1011,RESULT)
			LOCALS = 「대출중입니다」
			IF CFLAG:[[코스즈]]:현재위치 == CFLAG:PLAYER:현재위치
				CALL SPTALK, [[코스즈]], "通常", 0, @"%LOCALS%", 1
				SIF TARGET != [[코스즈]]
					RESETCOLOR
			ELSE
				CALL SPTALK, 0, "通常", 12, @"%LOCALS%", 1
			ENDIF
		ELSE
			SETBIT FLAG:1011,RESULT
			IF !CHARISMA
				MONEY -= 3000
			ELSE
				MONEY:2 -= 10
			ENDIF
			ITEM:(100 + RESULT) = 1
			FLAG:대본반환기한 = 8
			TFLAG:193 = 1
			PRINTFORML %조사처리(ITEMNAME:(100 + RESULT),"를")% 빌렸다
			LOCALS = 「감사합니다」/「대출 기간은 일주일간입니다」 
			IF CFLAG:[[코스즈]]:현재위치 == CFLAG:PLAYER:현재위치
				CALL SPTALK, [[코스즈]], "笑顔", 0, @"%LOCALS%", 1
				SIF TARGET != [[코스즈]]
					RESETCOLOR
			ELSE
				CALL SPTALK, 0, "笑顔", 12, @"%LOCALS%", 1
			ENDIF
			RETURN 1
		ENDIF
	ELSEIF RESULT == 0
		RETURN -1
	ELSE
		GOTO LOOP
	ENDIF
ENDIF
TIME += 30

;대본소
@COM_ABLE620
;일괄관리
SIF GLOBAL_COMABLE(620)
	RETURN RESULT
;貸本屋ではない場所
SIF !BOOK_RENTAL(CFLAG:MASTER:현재위치)
	RETURN 0
;수면중
SIF CFLAG:수면
	RETURN 0
;시간정지중
SIF FLAG:70
	RETURN 0
RETURN 1

;貸本屋を判定する関数
@BOOK_RENTAL(ARG)
#FUNCTION
;鈴奈庵
SIF ARG == GET_MAP_REPLACEMENT(213)
	RETURNF 1
RETURNF 0

@BOOKLEND
FOR LOCAL,1,BookTitles + 1
	SIF LOCAL == 6	;[그림연극]을 패스
		CONTINUE
	SIF !ITEM:(100 + LOCAL) && !RAND:3
		INVERTBIT FLAG:1011,LOCAL
NEXT
SIF FLAG:대본반환기한
	FLAG:대본반환기한 -= 1
IF FLAG:대본반환기한 == 1
	FOR LOCAL,1,BookTitles + 1
		ITEM:(100 + LOCAL) = 0
	NEXT
	PRINTL 반환 기한이 왔으므로, 빌리고 있던 책을 돌려주었습니다.
ENDIF
