;-------------------------------------------------
;가슴애무
;애무계 커맨드, 레벨 2
;-------------------------------------------------
@COM6
;-------------------------------------------------
;COM별 FLAG의 설정
;-------------------------------------------------
;행위자
TCVAR:116 = PLAYER
;봉사 쾌락
TCVAR:PLAYER:108 = 1

;-------------------------------------------------
;소스 추가 처리
;-------------------------------------------------
;소비 체력, 기력
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 20
	SOURCE:쾌B = 100
	IF ABL:PLAYER:손가락 >= 4
		SOURCE:쾌B += 70
	ELSE
		SOURCE:쾌B += ABL:PLAYER:손가락 * 15
	ENDIF
ELSE

	DOWNBASE:체력 += 5
	DOWNBASE:기력 += 50

	SOURCE:쾌B = 100
	SOURCE:사랑 = 50
	SOURCE:성행동 = 60
	SOURCE:노출 = 10
	SOURCE:불결 = 20
	SOURCE:반감 = 20
	;-------------------------------------------------
	;특수소스처리
	;-------------------------------------------------
	IF ABL:PLAYER:손가락 >= 4
		SOURCE:쾌B += 70
	ELSE
		SOURCE:쾌B += ABL:PLAYER:손가락 * 15
	ENDIF
	;억압, 저항 소유에는, 억울소스 추가
	SIF TALENT:자기애 < 0 || TALENT:저항
		SOURCE:울굴 += 20
	;-------------------------------------------------
	;더러움의 처리
	;-------------------------------------------------
	;노예의 가슴의 더러움이 없으면 입을 사용한다(애액과 모유 얼룩은 OK, 더러움 무시나 조수 조교시는 문제 없음)
	IF STAIN:5 < 2 || STAIN:5 == 16 || STAIN:5 == 17 || ASSIPLAY == 1 || TALENT:PLAYER:악취내성 == 2
		;조교자의 TALENT:혀놀림을 본다
		TIMES SOURCE:불결 , 1.40
		SIF TALENT:PLAYER:혀놀림
			TIMES SOURCE:쾌B , 1.40
		SOURCE:울굴 += SOURCE:불결 / 20
		;노예의 B⇔조교자의 입의 더러움이 이동
		STAIN:5 |= STAIN:PLAYER:구
		STAIN:PLAYER:구 |= STAIN:5
		EXP:PLAYER:펠라티오경험 ++
	ENDIF
ENDIF
;노예의 B⇔조교자의 손가락의 더러움이 이동
STAIN:5 |= STAIN:PLAYER:1
STAIN:PLAYER:1 |= STAIN:5

;-------------------------------------------------
;경험의 처리
;-------------------------------------------------
;동성애 레즈비언이라면 반환값 1 동성애자라면 반환값 2
IF FLAG:70 == 1
	IF HETEROSEX(TARGET, PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET, PLAYER)) += 5
	ENDIF
ELSE
	IF HETEROSEX(TARGET, PLAYER)
		EXP:(50 + HETEROSEX(TARGET, PLAYER)) += 5
		EXP:PLAYER:(50 + HETEROSEX(TARGET, PLAYER)) += 5
	ENDIF
ENDIF
EXP:PLAYER:수음경험 ++
RETURN 1

;-------------------------------------------------
;실행 판정
;-------------------------------------------------
@COM_ABLE6
;가슴애무 실행 판정
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(6)
	RETURN RESULT
RETURN 1

