;-------------------------------------------------
;기승위
;섹스계 커맨드, 레벨 2
;-------------------------------------------------
@COM65

;지난번 조교가 3P일 때 3P로
IF PREVCOM == 71
	CALL COM_ABLE71
	SIF RESULT
		JUMP COM71
ENDIF
;-------------------------------------------------
;COM별 FLAG의 설정
;-------------------------------------------------
;행위자
TCVAR:116 = TARGET
;봉사 쾌락 플래그
TCVAR:108 = 1

CSTR:PLAYER:10 += @"{TARGET}/"
;TCVAR:2 사정 장소 플래그(-1=콘돔 1=질내 2=애널 3=수음 4=펠라치오 5=파이즈리 6=스마타 7=풋잡 8=체표 9=애널봉사 10=도구 11=촉수 12=A고문 13=V고문)
TCVAR:PLAYER:2 = 1
TCVAR:PLAYER:동정상실 = 1
;강도
TCVAR:PLAYER:4 = 2000

CSTR:TARGET:10 += @"{PLAYER}/"
;TCVAR:2 사정 장소 플래그(-1=콘돔 1=질내 2=애널 3=수음 4=펠라치오 5=파이즈리 6=스마타 7=풋잡 8=체표 9=애널봉사 10=도구 11=촉수 12=A고문 13=V고문)
TCVAR:TARGET:2 = 13
;강도
TCVAR:TARGET:4 = 1300

DOWNBASE:PLAYER:체력 += 20
DOWNBASE:PLAYER:기력 += 40

;-------------------------------------------------
;소스 추가 처리
;-------------------------------------------------
;소비 체력, 기력
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 150
	SOURCE:PLAYER:쾌C = 400

	SOURCE:쾌V = 500
	SOURCE:고통 = 250

	;처녀일 경우, 고통 소스와 반발 소스를 추가한다
	IF TALENT:처녀 == 1
		SOURCE:고통 += 1000
	ENDIF

	;재생 처녀일 경우, 고통 소스와 반발 소스를 추가한다
	IF TALENT:처녀 == 2
		SOURCE:고통 += 250
	ENDIF

ELSE

	DOWNBASE:체력 += 80
	DOWNBASE:기력 += 50
	SOURCE:PLAYER:쾌C = 400

	SOURCE:쾌V = 500
	SOURCE:사랑 = 250
	SOURCE:성행동 = 250
	SOURCE:달성 = 250
	SOURCE:고통 = 500
	SOURCE:노출 = 120
	SOURCE:일탈 = 600
	SOURCE:반감 = 800

	;위에서 계산한 더러움 데이터
	SOURCE:불결 = Y*10 + 60
	CALL 특수소스처리
ENDIF
;V에의 고통은 먼저 처리해 둔다
CALL PAIN_CHECK_V(TARGET)

;-------------------------------------------------
;더러움 처리
;-------------------------------------------------
;사정에 관계없이 행해지는 처리는 여기에서
CALL STAIN_질내P삽입

;-------------------------------------------------
;경험 처리
;-------------------------------------------------
;성교경험
EXP:PLAYER:삽입경험 ++
EXP:V경험 ++
IF FLAG:70 == 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 7
	ENDIF
		EXP:PLAYER:시간경험 += 1
ELSE
	;이상경험
	IF TALENT:처녀 && TALENT:처녀 != -1 && !GETBIT(CFLAG:3,1)
		IF ASSIPLAY && (TALENT:PLAYER:2 & 1)
			EXP:이상경험 += 3
		ELSE
			EXP:이상경험 += 2
		ENDIF
		SETBIT CFLAG:3,1
	ENDIF
	;동성애 레즈라면 반환값 1 게이라면 반환값 2
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 7
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 7
	ENDIF
	EXP:V성교경험 ++
	IF !ASSIPLAY
		EXP:애정경험 += TALENT:연모 * 2
		;처녀
		SIF TALENT:처녀 && TALENT:처녀 != -1 && TALENT:연모
			EXP:애정경험 += 5
	ENDIF
ENDIF
RETURN 1

@CAN_COM65(ARG)
;-------------------------------------------------
;실행 판정 처리
;-------------------------------------------------
IF ARG == 1
	CALL ChkBeforeInsert(TARGET)
	IF RESULT == -1
		TFLAG:삽입거절 = 1
	ENDIF
ENDIF
SIF TFLAG:삽입거절
	RETURN 0
IF FLAG:70 == 1
	SKIPDISP ARG
	PRINTL 기승위로 범한다
ELSE
	SKIPDISP ARG
	PRINTL 기승위
	A = 0
	S = 0

	;모든 명령에 공통적인 요소 고려
	;(순종이 높으면 명령에 따르기 쉬움 등)
	CALL COM_ORDER

	;ABL:욕망
	IF ABL:욕망
		SIF S
			PRINT  + 
		A += ABL:욕망 * 3
		PRINT 욕망
		PRINTV 'LV,ABL:욕망,'(,ABL:욕망 * 3,')
		S = 1
	ENDIF
	;ABL:V감각
	IF ABL:V감각
		SIF S
			PRINT  + 
		A += ABL:V감각 * 2
		PRINT V감각
		PRINTV 'LV,ABL:V감각,'(,ABL:V감각 * 2,')
		S = 1
	ENDIF
	;ABL:봉사정신
	IF ABL:봉사정신
		SIF S
			PRINT  + 
		A += ABL:봉사정신 * 4
		PRINT 봉사정신
		PRINTV 'LV,ABL:봉사정신,'(,ABL:봉사정신 * 4,')
		S = 1
	ENDIF
	IF MARK:쾌락각인
		SIF S
			PRINT  + 
		A += MARK:쾌락각인 * 3
		PRINT 쾌락각인
		PRINTV 'LV,MARK:쾌락각인,'(,MARK:쾌락각인 * 3,')
		S = 1
	ENDIF

	;PALAM:윤활(의 부족)
	IF PALAM:윤활 < PALAMLV:3
		PRINT  - 
		A -= 5
		PRINT 윤활 부족
		PRINTV '(,5,')
		S = 1
	ENDIF
	;PALAM:욕정
	IF GETPALAMLV(PALAM:욕정,5)
		SIF S
			PRINT  + 
		A += GETPALAMLV(PALAM:욕정,5) * 3
		PRINT 욕정
		PRINTV 'LV,GETPALAMLV(PALAM:욕정,5),'(,GETPALAMLV(PALAM:욕정,5) * 3,')
		S = 1
	ENDIF
	;수줍음
	IF TALENT:수치심 > 0
		PRINT  - 
		A -= 2
		PRINT 수줍음
		PRINTV '(,2,')
		S = 1
	ENDIF
	;快感の否定（淫乱、娼婦で無効）
	IF TALENT:쾌감응답 < 0 && !TALENT:연모
		PRINT  - 
		A -= 5
		PRINT 쾌감에 부정
		PRINTV '(,5,')
		S = 1
	ENDIF
	;남성기피(연모, 친애로 무효)
	IF OTOKOGIRAI(TARGET)
		PRINT  - 
		A -= 12
		PRINT 남성기피
		PRINTV '(,12,')
		S = 1
	ENDIF
	;연모
	IF TALENT:연모 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 15
		PRINT 연모
		PRINTV '(,15,')
		S = 15
	ENDIF
	;친애
	IF TALENT:연모 > 1 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 5
		PRINT 친애
		PRINTV '(,5,')
		S = 5
	ENDIF


	;처녀
	LOCAL = MAX(10,40 - EXP:V경험)
	IF TALENT:처녀 && TALENT:처녀 != -1
		PRINT  - 
		A -= LOCAL
		PRINT 처녀
		PRINTV '(,LOCAL,')
		S = 1
	ELSEIF EXP:V경험 < EXPLV:2
		PRINT  - 
		A -= 15
		PRINT V경험 부족
		PRINTV '(,15,')
		S = 1
	ENDIF

		;조교자가 후타나리
	IF (TALENT:PLAYER:2 & 3) == 3
		SIF S
			PRINT  + 
		A += 4
		PRINT 후타나리
		PRINTV '(,4,')
		S = 1
	ENDIF

	;媚薬
	IF TCVAR:춘약
		SIF S
			PRINT  + 
		A += 6
		PRINT 춘약
		PRINTV '(,6,')
		S = 1
	ENDIF

	Y = 0
	;애액의 더러움
	SIF STAIN:PLAYER:2 & 1
		Y += 1
	;정액의 더러움
	SIF STAIN:PLAYER:2 & 4
		Y += 3
	;애널의 더러움
	SIF STAIN:PLAYER:2 & 8
		Y += 7
	SIF STAIN:PLAYER:2 & 16
		Y += 15

	Y = Y * (2 - TALENT:악취내성) / 2

	;더러움 있음
	IF Y
		PRINT  - 
		A -= Y
		PRINT 더러움 있음
		;악취 둔감
		IF TALENT:악취내성 > 0
			PRINT , 악취 둔감
		ELSEIF TALENT:악취내성 < 0
			PRINT , 악취 민감
		ENDIF
		PRINTV '(,Y,')
		S = 1
	ENDIF


	;합계를 표시(35이상으로 실행)
	PRINT  = 
	PRINTV A

	V = 35
	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 실행치
	PRINTV V

	WAIT
	SKIPDISP 0
	;실행할 수 없다
	SIF A < V
		RETURN 0
ENDIF
SKIPDISP 0
RETURN 1

;-------------------------------------------------
;실행 판정
;-------------------------------------------------
@COM_ABLE65
;騎乗位실행 판정
SIF !TFLAG:100
	RETURN 0
SIF GLOBAL_COMABLE(65)
	RETURN RESULT
;조교자가 남자, 후타나리인 경우는 어느 정도 발기하고 있지 않으면 안 됨
SIF (TALENT:PLAYER:2 & 2) && BASE:PLAYER:발기 < 1000
	RETURN 0
;바이브 삽입 중은 안 됨
SIF TEQUIP:13
	RETURN 0
;남자라면 안 됨
SIF !(TALENT:2 & 1)
	RETURN 0
;체격 차이가 있는 경우는 경험이 없으면 안 됨
SIF 삽입불가(TARGET) == 1 && EXP:V경험 < 50 && !TALENT:PLAYER:금단의지식
	RETURN 0
;더 체격 차이가 나면 금단의 지식이 필요함
SIF 삽입불가(TARGET) == 2 && (!TALENT:PLAYER:금단의지식 || EXP:V경험 < 50)
	RETURN 0
SIF 삽입불가(TARGET) == 3
	RETURN 0
;조교자가 남자, 반인반요, 후타나리가 아니면 안 됨
SIF !(TALENT:PLAYER:2 & 2)
	RETURN 0

;하반신착의
SIF TEQUIP:0 & 4
	RETURN 0
;욕실 플레이 중에는 플레이매트가 없으면 안 됨
IF TEQUIP:31
	SIF ITEM:20 == 0 && NOITEM == 0
		RETURN 0
ENDIF
;처녀에, 순종 4 이하・레즈끼 4 이하의 ASSI의 경우는 불가능하다(새드라면 ＯＫ)
IF TALENT:처녀 && ASSIPLAY
	SIF (ABL:ASSI:순종 <= 4 || ABL:ASSI:레즈끼 <= 4) && TALENT:ASSI:새드 == 0
		RETURN 0
ENDIF
;시간정지 중은 스킵
IF FLAG:70 != 1
	;처녀면서 윤활부족은 안 됨
	SIF PALAM:윤활 < PALAMLV:2 && TALENT:처녀 && TALENT:처녀 != -1
		RETURN 0
	;縄使用中は不可
	SIF TEQUIP:밧줄
		RETURN 0
ENDIF
;수면간 중
SIF CFLAG:장난 > 1
	RETURN 0
;침대가 없다 OR 욕실에서 플레이매트가 없으면 안 됨
SIF !BEDROOM(CFLAG:PLAYER:현재위치) && !(BATHROOM() && ITEM:20)
	RETURN 0
RETURN 1

