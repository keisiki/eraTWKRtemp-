;-------------------------------------------------
;뺨을쿡쿡 찌른다
;일상계 커맨드 레벨 1
;-------------------------------------------------
@COM308
SELECTCASE TFLAG:192
	;通常
	CASE 0
		LOCAL = RAND:110
		LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
		SIF LOCAL:1 > 109
			LOCAL:1 = 109
		IF LOCAL <= 9
			;大成功
			TFLAG:193 = 1
		ELSEIF LOCAL >= LOCAL:1
			;失敗
			TFLAG:193 = -1
		ELSE
			;残りは成功
			TFLAG:193 = 0
		ENDIF
	;強制成功or大成功
	CASE 1
		IF RAND:100 <= 9
			;大成功
			TFLAG:193 = 1
		ELSE
			;残りは成功
			TFLAG:193 = 0
		ENDIF
	;強制失敗
	CASE -1
		TFLAG:193 = -1
	;コマンド終了
	CASE -2
		TFLAG:193 = -1
		RETURN 0
ENDSELECT
;선제 발생시는 실패하지 않는다
SIF TFLAG:102 == 3 && TFLAG:192 != -1
	TFLAG:193 = MAX(TFLAG:193,0)

IF CFLAG:TARGET:동침중
	DOWNBASE:MASTER:기력 += 50
ELSE
	DOWNBASE:기력 += 50
	DOWNBASE:MASTER:기력 += 50
ENDIF

;고정으로 획득하는 소스
SOURCE:환락 = 80
SOURCE:사랑 = 50
SOURCE:반감 = 200

;ABL:친밀을 본다
IF ABL:친밀 <= 3
	SOURCE:환락 += 100 + (ABL:친밀 * 20)
	SOURCE:사랑 += 150 + (ABL:친밀 * 30)
ELSEIF ABL:친밀 <= 5
	SOURCE:환락 += 300 + (ABL:친밀 * 40)
	SOURCE:사랑 += 350 + (ABL:친밀 * 30)
ELSEIF ABL:친밀 <= 8
	SOURCE:환락 += 600 + (ABL:친밀 * 50)
	SOURCE:사랑 += 500 + (ABL:친밀 * 40)
ELSEIF ABL:친밀 <= 11
	SOURCE:환락 += 800 + (ABL:친밀 * 70)
	SOURCE:사랑 += 700 + (ABL:친밀 * 60)
ELSE
	SOURCE:환락 += 1200 + (ABL:친밀 * 40)
	SOURCE:사랑 += 1500 + (ABL:친밀 * 20)
ENDIF

;호감 도를 본다
IF CFLAG:2 <= 500
	SOURCE:환락 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:환락 += 200 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:환락 += 1000 + (CFLAG:2 - 5000) / 5
ENDIF
SIF SOURCE:환락 < 0
	SOURCE:환락 = 0

SOURCE:굴종 = 500
SOURCE:수동 = 100 + 200 * ABL:순종

IF TFLAG:193 == -1
	TIMES SOURCE:환락 , 0.10
	TIMES SOURCE:사랑 , 0.50
	TIMES SOURCE:수동 , 0.50
	TIMES SOURCE:굴종 , 1.50
	TIMES SOURCE:반감 , 1.50
ELSEIF TFLAG:193 == 0
	TIMES SOURCE:환락 , 1.00
	TIMES SOURCE:사랑 , 1.00
	TIMES SOURCE:수동 , 1.00
	TIMES SOURCE:굴종 , 1.00
ELSE
	TIMES SOURCE:환락 , 2.00
	TIMES SOURCE:사랑 , 2.00
	TIMES SOURCE:수동 , 2.00
	TIMES SOURCE:굴종 , 0.50
	TIMES SOURCE:반감 , 0.75
ENDIF
TIME += 10
RETURN 1

;-------------------------------------------------
;실행 판정
;-------------------------------------------------
@COM_ABLE308
;뺨을 쿡쿡 찌르는 실행 판정
SIF !TFLAG:100
	RETURN 0
;수면간중
SIF CFLAG:장난 > 1
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(308)
	RETURN RESULT
;수면중이 아니다
SIF !CFLAG:수면 && !CFLAG:동침중
	RETURN 0
;장난중이 아니다
SIF !CFLAG:장난 && !CFLAG:동침중
	RETURN 0
;업무중
SIF CFLAG:행동 == 5
	RETURN 0
;기력 0
SIF BASE:MASTER:기력 <= 0
	RETURN 0
RETURN 1
