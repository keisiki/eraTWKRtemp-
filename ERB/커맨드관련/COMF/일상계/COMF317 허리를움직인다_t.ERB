;-------------------------------------------------
;허리를움직인다(V)
;일상계 커맨드 레벨 3
;-------------------------------------------------
@COM317

IF ABL:MASTER:허리 >= 3
	CALL ASK_M("격렬하게", 1,"끈덕지게", 1,"그라인드", 1)
	TFLAG:194 = RESULT + 1

	IF EFFECTIVE_PATTERN_V:(TEQUIP:V콤보) == TFLAG:194
		TEQUIP:V콤보 ++
		TFLAG:193 = TEQUIP:V콤보
		SIF TEQUIP:V콤보 == 3
			TEQUIP:V콤보 = 0
	ENDIF
ENDIF
;-------------------------------------------------
;COM별 FLAG의 설정
;-------------------------------------------------
;행위자
TCVAR:116 = PLAYER

CSTR:PLAYER:10 += @"{TARGET}/"
;TCVAR:2 사정장소 플래그(-1=콘돔 1=질내 2=애널 3=수음 4=펠라티오 5=파이즈리 6=가랑이 7=풋잡 8=체표 9=애널 봉사 10=도구 11=촉수 12=A고문 13=V 꾸짖고)
TCVAR:PLAYER:2 = 1
;강도
TCVAR:PLAYER:4 = 500 + ABL:허리 * 50 + ABL:질 * 50

CSTR:TARGET:10 += @"{PLAYER}/"
;TCVAR:2 사정장소 플래그(-1=콘돔 1=질내 2=애널 3=수음 4=펠라티오 5=파이즈리 6=가랑이 7=풋잡 8=체표 9=애널 봉사 10=도구 11=촉수 12=A고문 13=V 꾸짖고)
TCVAR:TARGET:2 = 13
;강도
TCVAR:TARGET:4 = 1500

;-------------------------------------------------
;소스 추가 처리
;-------------------------------------------------
;소비 체력, 기력
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 80
	SOURCE:MASTER:쾌C = 40 + ABL:질 * 50
	SOURCE:쾌V = 40 + ABL:PLAYER:허리 * 30
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 4
	ENDIF
	EXP:V성교경험 ++
	EXP:PLAYER:삽입경험 ++
ELSE
	DOWNBASE:PLAYER:체력 += 50
	DOWNBASE:PLAYER:기력 += 80

	SOURCE:MASTER:쾌C = 40 + ABL:허리 * 50 + ABL:질 * 50
	SOURCE:쾌V = 50 + ABL:PLAYER:허리 * 20
	SOURCE:사랑 = 200
	SOURCE:고통 = 250
	SOURCE:욕정 = 250
	SOURCE:노출 = 150
	SOURCE:일탈 = 150
	SOURCE:반감 = 100

	;-------------------------------------------------
	;특수소스처리
	;-------------------------------------------------
	;억압, 저항 소유에는, 억울소스 추가
	IF TALENT:자기애 < 0 || TALENT:저항
		SOURCE:울굴 += 20
	ENDIF
	;V에의 고통
	CALL PAIN_CHECK_V(TARGET)
	;-------------------------------------------------
	;경험의 처리
	;-------------------------------------------------
	EXP:PLAYER:삽입경험 ++
	;동성애 레즈비언이라면 반환값 1 동성애자라면 반환값 2
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 4
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 4
	ENDIF
	EXP:V성교경험 ++
	;-------------------------------------------------
	;콤보
	;-------------------------------------------------
	IF TFLAG:193 == 2
		SOURCE:쾌V += 10 + ABL:PLAYER:허리 * 15
		SOURCE:욕정 += 100
		SOURCE:굴종 += 100
		SOURCE:사랑 += 50
		TCVAR:PLAYER:4 += ABL:질 * 50
		EXP:V성교경험 ++
	ELSEIF TFLAG:193 == 3
		SOURCE:쾌V += 50 + ABL:PLAYER:허리 * 30
		SOURCE:욕정 += 200
		SOURCE:굴종 += 300
		SOURCE:사랑 += 200
		TCVAR:PLAYER:4 += ABL:질 * 150
		SIF ABL:V감각 >= 5
		SOURCE:쾌C += 30
		EXP:V성교경험 ++
		TFLAG:호감도보너스 = 100
	ENDIF
	TIME += 5
ENDIF
;-------------------------------------------------
;더러움의 처리
;-------------------------------------------------
;노예의 질내⇔조교자의 P의 더러움이 이동
STAIN:PLAYER:2 |= STAIN:3
STAIN:PLAYER:2 |= STAIN:6
STAIN:3 |= STAIN:PLAYER:2
STAIN:6 |= STAIN:PLAYER:2
RETURN 1

;-------------------------------------------------
;실행 판정
;-------------------------------------------------
@COM_ABLE317
;실행 판정
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(317)
	RETURN RESULT
;정지 중에는 불가
SIF FLAG:70
	RETURN 0
;친해져 강도 2
SIF (TCVAR:MASTER:친해져 < 2) && CFLAG:수면 == 0
	RETURN 0
;착의
SIF TEQUIP:0 & 4
	RETURN 0
;수면중
SIF CFLAG:수면 && CFLAG:장난 < 2
	RETURN 0
;업무중
SIF CFLAG:행동 == 5
	RETURN 0
;데이트중
SIF CHK_DATENOW(CFLAG:데이트중) && CFLAG:현재위치 != OMANEKIBEYA()
	RETURN 0
;동침중
SIF CFLAG:동침중
	RETURN 0
SIF TCVAR:TARGET:100 != PLAYER
	RETURN 0
;기력 0
SIF BASE:MASTER:기력 <= 0
	RETURN 0
SIF GETBIT (TCVAR:26,8)
	RETURN 0
SIF CFLAG:우후후 != 2
	RETURN 0
;TARGET가 삽입되지 않았다
SIF TEQUIP:V접촉부위 != 1
	RETURN 0
RETURN 1

