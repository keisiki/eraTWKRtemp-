;-------------------------------------------------
;弾幕勝負
;日常系コマンド、レベル1
;-------------------------------------------------
@COM480

#DIM OPPONENT_REMAIN
#DIM FirstWinBonus
#DIM BattleEXP

HANDICAP_FIXED = 0
HANDICAP_RAND = 0
TFLAG:GipsLV = 0

IF TCVAR:TARGET:탄막승부불능
	PRINTFORML %타겟은% 이미 많이 지친 것 같다. 내일 다시 신청하자.
	RETURN -1
ENDIF

$INPUT_LOOP
CALL DANMAKU_OPPONENT_STATUS(TARGET)

PRINTFORM %CALLNAME:TARGET%에게 탄막 승부를 신청합니까?
SIF CFLAG:TARGET:탄막승부승리 == 1
	PRINTFORM 　★승리함
PRINTL 
CALL ASK_M("역시 그만둔다", 1, "도전한다", 1, "룰 설명", 1, "깁스 장착", (ITEM:슈터깁스 || ITEM:루나슈터양성깁스))
SELECTCASE RESULT
	CASE 0
		TFLAG:GipsLV = 0
		RETURN -1
	CASE 1
		
	CASE 2
		CALL SHOW_DANMAKU_RULE
		GOTO INPUT_LOOP
	CASE 3
		CALL DANMAKU_GipsEquip
		GOTO INPUT_LOOP
ENDSELECT

;台詞・選択肢は幽香口上内に記述、口上がない場合は賭けや勝利報酬は無し
;ここでHANDICAP_FIXED, HANDICAP_RANDに数値を入れると、あなたのダイス判定にマイナス補正がかかるよ
;判定値 -= (HANDICAP_FIXED + RAND:(HANDICAP_RAND))
VARSET RESULT
CALL KOJO_MESSAGE_SEND("DANMAKU", 0, TARGET, -1, -1, "戦闘前")
IF RESULT == -1
	RETURN -1
ENDIF


PRINTFORMW 전투 개시!!
CALL DANMAKU_BATTLE(TARGET)
OPPONENT_REMAIN = RESULT

CALL KOJO_MESSAGE_SEND("DANMAKU", OPPONENT_REMAIN, TARGET, -1, -1, "戦闘後")
PRINTL 

;source操作他
TCVAR:TARGET:탄막승부불능 = 1


DOWNBASE:기력 += 500
DOWNBASE:MASTER:기력 += 500
DOWNBASE:체력 += 500
DOWNBASE:MASTER:체력 += 500

SOURCE:환락 += (3000 + ABL:친밀 * 100) / (OPPONENT_REMAIN + 1)

IF !CFLAG:TARGET:탄막승부승리 && OPPONENT_REMAIN == 0
	CALL COLORMESSAGE(@"【%CALLNAME:TARGET%에게 첫 승리】",C_YELLOW,1)
	TFLAG:호감도보너스 = 400 / (OPPONENT_REMAIN + 1)
	TFLAG:신뢰도관리 = 3 - OPPONENT_REMAIN
	CFLAG:TARGET:탄막승부승리 = 1
	SELECTCASE ABL:TARGET:전투능력
		CASE 0
			FirstWinBonus = 1
		CASE 1
			FirstWinBonus = 3
		CASE 2
			FirstWinBonus = 5
		CASE 3
			FirstWinBonus = 10
		CASE 4
			FirstWinBonus = 20
		CASE 5
			FirstWinBonus = 30
		CASEELSE
			FirstWinBonus = 50
	ENDSELECT
	EXP:MASTER:전투경험 += FirstWinBonus
ENDIF

BattleEXP = 5 - OPPONENT_REMAIN
IF ABL:MASTER:전투능력 <= ABL:TARGET:전투능력
	BattleEXP *= 2
	CALL COLORMESSAGE(@"【강적 보너스로 취득 경험이 업】",C_YELLOW,1)
ENDIF
EXP:MASTER:전투경험 += BattleEXP
TIME += 60

SIF AT_HOME(MASTER)
	YOGORE:(CFLAG:MASTER:현재위치) = LIMIT(YOGORE:(CFLAG:MASTER:현재위치) + 3000, 0, 6000)

RETURN 1


;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE480
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(480)
	RETURN RESULT
SIF TARGET <= 0
	RETURN 0
SIF !SHIRAHU(TARGET)
	RETURN 0
SIF CFLAG:우후후
	RETURN 0
;仕事中
SIF CFLAG:행동 == 5
	RETURN 0
;전투능력0以外
SIF ABL:TARGET:전투능력 == 0
	RETURN 0
SIF !전투훈련가능(CFLAG:MASTER:현재위치)
	RETURN 0
;体力・気力不足
SIF BASE:MASTER:기력 < 500 || BASE:MASTER:체력 < 500
	RETURN 0
;体力・気力不足
SIF BASE:TARGET:기력 < 500 || BASE:TARGET:체력 < 500
	RETURN 0
RETURN 1
