;-------------------------------------------------
;루코토와 대화
;일상계 커맨드, 레벨 1
;-------------------------------------------------
@COM420
PRINTL 「뭔가 용무가 있으신가요?」
PRINTL [0] - 사람을 찾고 있다
PRINTL [1] - 청소를 하고 싶다
PRINTL [2] - git에 대해
PRINTL [100] - 돌아간다
INPUT
IF RESULT == 0
	PRINTL 「누구를 찾으세요?」
	CALL HITOSAGASI()
ELSEIF RESULT == 1
CALL OHEYA_CHECK
ELSEIF RESULT == 2
	PRINTDATA
		DATAFORM 「git을 사용하면 패치를 한 방에 Merge할 수 있어요」
		DATAFORM 「git이 있으면 변경한 곳을 쉽게 파악 가능해져요」
		DATAFORM 「과거의 코드도 확실하게 보존해주고요」
	ENDDATA
	PRINTW 아무래도 굉장한 것인가 보다
	RETURN -1
ELSEIF RESULT == 100
	RETURN -1
ELSE
	RETURN -1
ENDIF
INPUTS
RESULT = TOINT(RESULTS)
IF RESULT == 0
	REPLACE RESULTS, CALLNAME:MASTER, "%CALLNAME:MASTER%"
	FOR LOCAL, 1, 19
		SIF STR:LOCAL == RESULTS
			RESULT = LOCAL
	NEXT
ENDIF
SIF RESULT == 0
	RETURN -1
IF FLAG:창고잠금
	PRINTW 잠금을 풀지 않으면 
	RESTART
ENDIF
IF (RESULT >= 26 && RESULT <= 35) && ABL:MASTER:교양 < 1 && !MAIN_MAP && !INRANGE(CFLAG:MASTER:초기위치, 26,35)
	PRINTFORMW 미숙한 %마스터로%선 찾아낼 수 없다…
ELSE
	IF IN_HOME(RESULT) == 1
		;NEXTCOM = GETNUM(TRAINNAME, "이동")
		;TFLAG:195 = RESULT
		CALL SKIP_MOVE(CFLAG:MASTER:현재위치, RESULT)
	ENDIF
ENDIF
RETURN -1

@OHEYA_CHECK
VARSET LOCAL
TFLAG:이동불능메시지 = 0
FOR LOCAL,MINROOM(),MAXROOM
	LOCALS = %NAME_FROM_PLACE(LOCAL)%
	SIF LOCAL == CFLAG:MASTER:311
		LOCALS = %CALLNAME:MASTER% 방
	SIF LOCALS == ""
		GOTO SYOURYAKU
	SIF CFLAG:MASTER:현재위치 == LOCAL
		SETCOLOR 0 , 255, 0
	PRINTBUTTON "["+LOCALS+"]", LOCAL
	RESETCOLOR
	PRINTFORM %" "*(22-STRLENS(LOCALS))%
	LOCALS = %SEIKETU(LOCAL)%
	PRINTFORM %LOCALS,15,RIGHT%     
	RESETCOLOR
	LOCAL:1 ++
	SIF LOCAL:1 % 2 == 0
		PRINTL
	$SYOURYAKU
NEXT
PRINTFORML 
DRAWLINE
PRINTBUTTON "[확인 종료]", 0

@SEIKETU(ARG)
#FUNCTIONS
IF OUTROOF(ARG) && DAY:2 == 4
	SELECTCASE YOGORE:ARG
		CASE IS < 100
			SETCOLOR C_GREEN
			RETURNF "적설량:깔끔"
		CASE IS < 500
			SETCOLOR C_M_GREEN
			RETURNF "적설량:소"
		CASE IS < 1000
			SETCOLOR C_P_GREEN
			RETURNF "적설량:약간 소"
		CASE IS < 2000
			SETCOLOR 0xFFFFFF
			RETURNF "적설량:중"
		CASE IS < 3500
			SETCOLOR 0xFFFFFF
			RETURNF "적설량:약간 대"
		CASE IS < 5000
			SETCOLOR 0xFFFFFF
			RETURNF "적설량:대"
		CASEELSE
			SETCOLOR 0xFFFFFF
			RETURNF "적설량:눈더미"
	ENDSELECT
ELSE
	SELECTCASE YOGORE:ARG
		CASE IS < 100
			SETCOLOR C_GREEN
			RETURNF "청결도:최고"
		CASE IS < 500
			SETCOLOR C_M_GREEN
			RETURNF "청결도:고"
		CASE IS < 1000
			SETCOLOR C_P_GREEN
			RETURNF "청결도:약간 고"
		CASE IS < 2000
			SETCOLOR 0xFFFF00
			RETURNF "청결도:중"
		CASE IS < 3500
			SETCOLOR 0xFF00FF
			RETURNF "청결도:약간 저"
		CASE IS < 5000
			SETCOLOR 0xFF00FF
			RETURNF "청결도:저"
		CASEELSE
			SETCOLOR 0xFF0000
			RETURNF "청결도:쓰레기산"
	ENDSELECT
ENDIF

@HITOSAGASI(ARG)
IF ARG
	FOR LOCAL,1,CHARANUM
		SIF CFLAG:LOCAL:신사거주 > 0
			CONTINUE
		SIF AT_HOME(LOCAL) == 1
			CONTINUE
		SIF CFLAG:LOCAL:현재위치 == MAXROOM && CFLAG:LOCAL:수면
			CONTINUE
		FOR LOCAL:1,10,100,10
			IF CFLAG:LOCAL:현재위치 == ARG * 100 + LOCAL:1
				PRINTFORML %CALLNAME:LOCAL,15,LEFT%
				LOCAL:2 ++
			ENDIF
		NEXT
	NEXT
	SIF !LOCAL:2
		PRINTFORML 아무도 없다
ELSE
	FOR LOCAL,1,CHARANUM
		SIF AT_HOME(LOCAL) == 0
			CONTINUE
		SIF CFLAG:LOCAL:현재위치 < MINROOM()
			CONTINUE
		SIF CFLAG:LOCAL:현재위치 == MAXROOM && CFLAG:LOCAL:수면
			CONTINUE
		SIF CFLAG:LOCAL:신사거주
			SETCOLOR 160, 210, 255
		PRINTFORM %CALLNAME:LOCAL,15,LEFT%
		RESETCOLOR
		LOCALS = %NAME_FROM_PLACE(CFLAG:LOCAL:현재위치)%
		SIF CFLAG:LOCAL:현재위치 == CFLAG:MASTER:311
			LOCALS = %CALLNAME:MASTER% 방
		PRINTBUTTON "["+LOCALS+"]", CFLAG:LOCAL:현재위치
		PRINTFORM %" "*(22-STRLENS(LOCALS))%
		IF LOCALS == ""
			PRINTFORML 알 수 없는 위치에 있습니다({CFLAG:LOCAL:현재위치})
		ELSEIF CFLAG:LOCAL:수면
			PRINTL 취침중
		ELSEIF CFLAG:LOCAL:행동 == 5
			IF CFLAG:LOCAL:현재위치 == TFLAG:연회장 && TIME_PROGRESS(TIME:1) && (CFLAG:LOCAL:직종 == 49 || CFLAG:LOCAL:직종 == 51)
				PRINTL 연회중
			ELSE
				PRINTL 일하는중
			ENDIF
		ELSE
			PRINTL
		ENDIF
	NEXT
ENDIF
PRINTBUTTON "[확인 종료]", 0

;-------------------------------------------------
;실행 판정
;-------------------------------------------------
;루코토와이야기한다
@COM_ABLE420
;자택외는 불가
SIF AT_HOME(MASTER) == 0
	RETURN 0
;정지 중에는 불가
SIF FLAG:70 == 1
	RETURN 0
;이동 실행 판정
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(420)
	RETURN RESULT
SELECTCASE MAIN_MAP
	CASE 0
		;루코토가 자고 있으면 안 됨
		SIF CFLAG:2:수면
			RETURN 0
		;루코토가 있는 곳
		SIF CFLAG:MASTER:현재위치 != CFLAG:2:현재위치
			RETURN 0
	CASE 3
		SIF CFLAG:MASTER:현재위치 != 323
			RETURN 0
	CASE 9
		SIF CFLAG:MASTER:현재위치 != 923
			RETURN 0
	CASEELSE
		RETURN 0
ENDSELECT

SIF CFLAG:우후후 == 2
	RETURN 0
RETURN 1

